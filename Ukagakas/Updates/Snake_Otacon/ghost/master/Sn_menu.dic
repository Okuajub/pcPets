//---------------------------Menus--------------------------
//This .dic file is primarily concerned with the double-click menus for each of your ghosts. If you don't want to have menus for your characters, you'd just have to delete the OnMouseDoubleClick functions near the end of mouse.dic and then ignore this file, as none of it will be called I believe. Menus can be cool though, since you can put stuff in them and create new functions if you know what you're doing. For example, I've added a lot of games and interactions and little linked icons to my Hunter Smoker ghost to keep track of their feelings and such. Much of that is pretty complicated though (and kind of messy, cough), so I won't go into it here, but I covered some of the concepts in the coding page of the walkthrough, and enterprising ghost makers can study the menu.dic file I set up for Hunter/Smoker and see how I put it together.
//Anyway, menu.dic also contains the settings for a config menu that will let your user change their name or pronouns if they want, as well as the talk rate of the ghost, all of which are useful things for your ghost to do. So think hard about whether or not you want to skip this file or not.

//  __             _        
// / _\_ __   __ _| | _____ 
// \ \| '_ \ / _` | |/ / _ \
// _\ \ | | | (_| |   <  __/
// \__/_| |_|\__,_|_|\_\___|
//~~~~*Snake's Menu*~~~~~~~~~~~~~~~~~~~~~~~~
//--OpenSakuraMenu
//Things your Sakura will say when opening the menu.

OpenSakuraMenu
{
		"\0\s[0]\b2Need something?"
		"\0\s[0]\b2What needs to be done?"
		"\0\s[0]\b2 Mhmm."
		--
		MenuRun
}

//--MenuRun
//As you just saw above. This actually fills out the rest of the menu. If you're going to be adding or changing functions, this is where you'll be doing your edits.

MenuRun
 {	
 	wd = GetWeekDay
 	ap = GetAP
 	nowmin = Getnowmin
 	nowhour = Getnowhour
	//These four statements basically get the day of the week, whether it's AM or PM, the hour, and the time. It's putting together the information to display the time at the bottom of the balloon. Pay it no mind.
	--
	"\n\n\_q\b2" //The \n\n means two new lines as detailed in the walkthrough, and \_q means quick session, meaning all the text from this point on will display instantly instead of a few letters at a time like normal ghost dialogue.
	--
	if lastTalk != "" //What this does is check to see if the ghost has already said a random bit of dialogue on its own, essentially by seeing if the lastTalk thing is NOT empty (!= means "does not equal").
	{
	  "\![*]\q[Repeat Last Dialogue,OnLastTalk]\n\n[half]"
	}
	--
 	"\![*]\q[Talk to me.,OnAiTalk]\n"
	"\![*]\q[What are you thinking about?,OnAiTalk]\n"

	--
	"\![*]\q[Interact,interactSAKURA]\n"
	--
	if snakeFeelings > 20
	{
		"\![*]\q[Missions,missionSearch]\n"
	}
	--
	"\![*]\q[Config Menu,configmenu]\n/
	\![*]\q[Functions,dosomething]\n\n[half]/
 	\![*]\q[Nevermind,CANCEL]\n\n\n[half]%(wd) %(nowhour):%(nowmin) %(ap), %(month)/%(day)/%(year)\e"
	
	//These three lead to the Config Menu, the Function menu, and Cancel. You can leave these alone if you like. All the envelopes at the end (see word.dic) basically display the year, month, day, weekday, and time.
}

//Leave this alone
OnLastTalk 
{
"%(lastTalk)\e"
}

//This is figuring out what day of the week it is. Leave it alone.
GetWeekDay
{
	if weekday == 0
	{"Sun"}
	elseif weekday == 1
	{"Mon"}
	elseif weekday == 2
	{"Tue"}
	elseif weekday == 3
	{"Wed"}
	elseif weekday == 4
	{"Thu"}
	elseif weekday == 5
	{"Fri"}
	else
	{"Sat"}
}

//This is finding out whether it's AM or PM. Leave it alone.
GetAP
{
 	if hour >= 0 && hour <= 11
 		{"AM"}
 	else
 		{"PM"}
}

//This is figuring out the minute. Leave it alone.
Getnowmin
{
	if minute >= 0 && minute <=9
		{"0%(minute)"}
	else
		{"%(minute)"}
}

//This is figuring out the hour. Leave it alone.
Getnowhour
{
	if hour12 >= 0 && hour12 <=9
		{"0%(hour12)"}
	else
		{"%(hour12)"}
}
Select.interactSAKURA //Interaction menu for Snake. Gift him things, talk to him, etc.
{
	"\0\s[8]\b2Hm?\n\n/
	\_q\f[italic,true]Give...\f[italic,default]\n/
	\![*]\q[Cigarettes,giveCigs]\n/
	\![*]\q[Ration,giveRation]\n/
	\![*]\q[Ketchup,giveKetch]\n"
	--
	if snakeFeelings >= 20
	{
		"\![*]\q[A Book,giveBook]\n"
	}
	--
	"\n[half]"
	--
	if snakeFeelings <= -50
	{
		"\f[italic,true]Say...\f[italic,default]\n/
		\![*]\q[An Apology,sayApology]\n"
	} 
	elseif snakeApologies >= 3
	{
		"\f[italic,true]Say...\f[italic,default]\n/
		\![*]\q \f[strike,true]An Apology\f[strike,default] Snake won't accept that lie again.\n"
	}
	--
	"\n"
	--
	"\f[italic,true]Ask...\f[italic,default]\n/
	\![*]\q[About Shadow Moses,askShadowMosesIncident]\n"
	--
	if clickShadowMosesIncident > 0
	{
		"\![*]\q[About Liquid Snake,askLiquidSnake]\n"
	}
	--
	if snakeFeelings > 50 || askLove > 0
	{
		"\![*]\q[About Love Life,askLove]\n"
	}
	--
	if clickJaeger >= 1
	{
	"\![*]\q[About Jaeger,askJaeger]\n"
	}
	--
	if clickBigBoss >= 1
	{
	"\![*]\q[About Big Boss,askBigBoss]\n"
	}
	--
	"\![*]\q[About Otacon,askOtacon]\n\n[half]/
	\![*]\q[Nevermind,BACKTOMENU]\n\n\n/
	\f[italic,true]Inventory\n/
	Ketchup %(snakeKetchups)/20\f[italic,default]/
	\e"
}

//---- Snake Cigs ---------------------------------------------------------
//Giving Snake cigarettes.
Select.giveCigs
{
	
	if snakeFeelings <= -50
	{
		"\1\s[31]\0\s[8].\w8.\w8.\w8I don't know why I should trust these won't kill me.\w8\w8\w8\nBut okay.\w8\w8\w8\n\n[]\f[italic,true]Snake stares at the pack for a moment, hand slightly reached out, before snatching them up and shoving them in his bags as if they would burn him if held too long. Otacon continues to glare at you. But Snake seems slightly appeased.\e"
	}
	elseif snakeFeelings <= -21 && snakeFeelings >= -49
	{
		"\1\s[31]\0\s[9]...\w8\w8\n\n\f[italic,true]Snake hesitantly takes the cigarettes with a grunt. Otacon gives you a short glare. It... worked?\f[italic,default]\e"
	}
	elseif snakeFeelings >= -20 && snakeFeelings <= 20 && otaconFeelings >= -9
	{
		"\0\s[0]Huh. Thanks. I wasn't excpecting that. \1\s[17]%(username)! You know I'm trying to get him to quit! \0\s[5]\n\n[half]Well, I have to get them from somewhere, don't I?\1\s[31]Ughh.\0\s[5]\n\n\f[italic,true]This won't please both of them, but Snake seems very satisfied...\e"
	}
	elseif snakeFeelings >= 20 && snakeFeelings <= 50
	{
		"\0\s[5]Thanks, %(username), you know my favorite brand. "
		--
		"Moslems, huh?"
		"Hope, hm?"
		"Lucky Strikers, old school, huh?"
		"The Boss, hm? Nice quality."
		--
		"\1\s[33]...\0\s[5]\n\n\f[italic,true]This won't please both of them, but Snake seems very satisfied...\e"
	}
	elseif snakeFeelings >= 51 && otaconFeelings >= -19
	{
		"\0\s[5]Thanks, %(username)!\1\s[31]\w8\w8\w8\w8\w8\0\s[4]\n\n[half]\w8\w8\w8Sorry,\w4 Hal.\1\s[14]\w8\w8\w8I just worry about your health, Dave.\w8\w8 You're not invincible.\e"
	}
	elseif snakeFeelings >= -20 && snakeFeelings <= 20 && otaconFeelings <= -10
	{
		"\0\s[0]Huh. Thanks. I wasn't excpecting that. \1\s[17]%(username)! You know I'm trying to get him to quit! \0\s[5]\n\n[half]Well, I have to get them from somewhere, don't I?\1\s[31]\n\n[half]Ughh. You two are plotting against me.\0\s[5]\n\n\f[italic,true]This won't please both of them, but Snake seems very satisfied...\e"
	}
	elseif snakeFeelings >= 51 && otaconFeelings <= -30 && snakeApologies == 0
	{
		"\0\s[5]Thanks, %(username)!\1\s[33].\w8\.\w8\.\w8I can't believe you, %(username).\1\s[14]\w8\w8 You too, Dave.\0\s[4]\n\n[half]...Hal.\1\s[31]\n\n[half]\w8\w8\1\s[33]\w8\w8\w8 Look. I can't make you quit, David. I'm usually teasing you, anyway-\1\s[14] But you can stop standing with %(username)! You know what %(heshes) done to me.\w8\w8\nYou can make that decision for yourself.\w8\w8 I won't bug you about this anymore anyway, Snake."
		snakeFeelings -= 10
		snakeApologies++ //I didn't want to create a new variable and so I'm using Snake apologies. It makes sense Otacon wouldn't say this if Snake had also been mistreated, and it will make Snake harsher to judge the player should they mistreat him in the future.
	}
	snakeFeelings += 5
	otaconFeelings -= 3
}

//---- Snake MCIs ---------------------------------------------------------
//Giving Snake Rations. lol now i have a purpose for giving ketchup a variable.
Select.giveRation
{
	"\0\s[8]\f[italic,true]You hand Snake an MCI ration.\n\n\f[italic,default]"
	--
	if snakeKetchups >= 1 && snakeKetchups <= 9
	{
		"\0\s[5]Hey, I can use that ketchup on this. Thanks.\e"
		"\0\s[5]Ketchup mutes the taste a little. Thanks."
		"\0\s[5]Nice, the ketchup you gave me will work well with the meatloaf can. If I drown it, anyway."
		"\0\s[5]Ketchup and tuna... I've done worse. It works. Thanks."
		"\0\s[5]I never liked ketchup with my eggs, but I'll take it with one of these rations. Thanks."
		snakeKetchups--
		snakeFeelings += 2
	}
	elseif snakeKetchups >= 10 //This is to prevent a user trying to exploit it by giving him a lot of ketchup, the extra friendship point won't count until the last ten. This still gives a modest 30 points, but it technically only saves 10 clicks, so its not that over powered or anything. Combined with the ketchup cap, i think its fine.
	{
		"Jeez, I finally have a use for all that ketchup you gave me.\e"
		snakeKetchups--
	}
	"\0\s[8]Uh, thanks? \f[size,8]Do I even need to eat here..?\f[size,default]\e"
	"\0\s[8]Great. Never liked the taste... It gets you by.\e"
	"\0\s[8]Tastes like pennies and spam.\e"
	"\0\s[9]Ugh... This ones beans and fuckers.. ham and lima beans, if you want. All you need to know is it tastes like dog food.\e"
	"\0\s[0]Reminds me of Shadow Moses. Could use a little ketchup.\e"
	"\0\s[0]Edible.\w8\w8 Wait a minute, where's the D can? Hsh, I bet you took it.\e"
	"\0\s[8]Some people would call this food. Where do you get these things, huh? I could have sworn MREs were already the majority of rations.\e"
	"\0\s[5]Nice, this one's spaghetti and meatballs. One of the least offensive."
	"\0\s[9]These things have to be left over from Vietnam or something."
	if snakeFeelings > 40
	{
	"\0\s[9]This type of ration came in handy during Zanzibar. Not in the way you'd expect though."
	}
	--
	snakeFeelings++
}

//---- Snake Sups ---------------------------------------------------------
//Giving Snake Ketchup.
Select.giveKetch
{
	if snakeKetchups < 20
	{
	snakeKetchups++
	}
	--
	"\0\s[8]\f[italic,true]You hand Snake a bottle of ketchup.\f[italic,default]\n\n[half]"
	--
	if snakeKetchups == 1
	{
		"Uh, thanks?"
		snakeFeelings++
	}
	elseif snakeKetchups == 2
	{
		"I already had one, but, thanks."
		snakeFeelings++
	}
	elseif snakeKetchups >= 3 && snakeKetchups <= 9
	{
		"This is getting weird."
		"What's this all about?"
		"Uh, okay."
		"Where do you get all this stuff from?"
		"This just isn't right."
		"Do you have some kind of obsession?"
		".\w2.\w2.\w2Okay."
		"I guess it's a nice color."
		"Uh. Otacon, do you have any room for this?\1\s[18]Er... I don't think so. Sorry.\0\s[8]"
	}
	elseif snakeKetchups >= 10 && snakeKetchups <= 19
	{
		"\1\s[18]\0\s[8]I think you're freaking Otacon out."
		"What's all this for?"
		"You have all this ketchup for a reason?"
		"Why are you giving me your personal ketchup collection?"
		"Is this some kind of game to you?"
		"Do you use this for something?"
		"\f[italic,true]Snake takes the bottle without saying anything.\f[italic,false]"
		"I don't think we even have anything but rations to put this on, %(username)."
		"Aren't you getting tired of this?"
		"...Do you expect me to do a little song and dance for you?"
		"I don't need this."
	}
	elseif snakeKetchups >= 20
	{
		"\f[italic,true]Snake gives you an irritated, weirded out look.\f[italic,default]\n\n[half]"
		--
		"I already gave some to Otacon. I can't fit any more of this stuff in my pockets."
		"I don't have any room left."
		"Come on, don't I have enough already?"
		"Oh, I get it. You're trying to get a rise out of me, huh?"
		
		snakeFeelings--
	}
	else
	{
		"\f[italic,true]You've somehow managed to give Snake negative ketchup. He hasn't seemed to have noticed, though. Can he even see negative amounts of ketchup? *You* can, probably, you code sleuth! (or if you encounter this in normal play, er... thats a bug. report it for me?)"
	}
	--
	"\e"
}

//---- Snake Books ------------------------------------------------------
Select.giveBook
{
	"\0\s[8]Hm?\n\n[half]\f[italic,true]You hand Snake a book. "
	--
	if snakeFeelings >= 20
	{
		"It's a soft paperback of "
		"It's a well-worn second-hand copy of "
	}
	if snakeFeelings >= 30
	{
		"It's an old library copy of "
		"It's a thin pulp paper copy of "
	}
	if snakeFeelings >= 50
	{
		"It's a used hardback copy of "
	}
	if snakeFeelings >= 60
	{
		"It's a brand new hardback copy of "
	}
	--
	"\f[italic,true]"
	--
	if snakeFeelings >= 20
	{
		"Warrior Cats: Into the Wild. You reassure him it has lots of fun fight scenes. Snake reads the back cover.\n\n[half]\f[italic,default]Cats?.. I'm more of a dog person.\w8\w8 What the hell. I'll give it a read.\1\s[16]\f[italic,true]You hear Otacon snicker. Clearly he thinks its amusing."
		"a gay werewolf romance novel. It seems pretty steamy. Snake laughs before pausing to look at you.\f[italic,default]\n\n[half]\0\s[9]... You're serious? Look, you can read all the trashy monster-fucking novels you want, but it isn't exactly gifting material. I'll keep it to humor you."
		"a non-fiction Navy SEAL biography.\f[italic,default]\n\n[half]What? Are you saying I need to do some homework?\1\s[31]..Well, to be completely fair to %(username)... You did get caught red-handed by Raiden faking it.\0\s[9]\n\n[half]Ghh.."
		"the Minecraft Redstone Handbook.\f[italic,default]\n\n[half]What is this? I don't know what 'Minecraft' is.\1\s[15]Oh! Snake, hand that one over!\0\s[8]\f[italic,true]\n\n[half]Snake withholds it from Otacon.\f[italic,default]\n\n[half]Explain to me what it is first, brainiac.\1\s[19]\n\n[half]Alright, alright. But only if you're really going to give it to me.\w8\w8 Redstone powers machines and circuitry in Minecraft, a video game. Its pretty much like programming but.. in 3D? Somebody made Pokemon Red in it-\0\s[9]\n\n[half]Alright, you can stop there. I'm sufficiently convinced its nerd stuff. Here.\1\s[16]\f[italic,true]\n\n[half]Otacon looks satisfied with himself and thumbs through the book for a few moments before stuffing it away somewhere."
		--
		snakeFeelings += 5
	}
	if snakeFeelings >= 30
	{
		"a non-fiction book about an investigative reporter's work on the CIA.\f[italic,default]\n\n[half]Hm. I used to work for the CIA you know. Undercover agent. When I was there they were still post-Cold-War peacetime softies.\w8\w8 Can't hurt to revisit old memories."
		snakeFeelings += 6
	}
	if snakeFeelings >= 40
	{
		"a fantasy novel about espionage. It feels slightly more his style. He flips through a few pages.\n\n[half]\f[italic,default]Fantasy? Never really been a fan.\n\n[half]\f[italic,true]Apparently not.\f[italic,default]\n\n[half]Hmm.. Looks like it could be exciting. I'll try it.\n\n[half]\f[italic,true]Oh, maybe it's okay?"
		"a British comedy storybook about the animals who live on the bank of the river Thames.\n\n[half]\f[italic,default]Hey, I read this story as a kid. It's pretty funny. I think the water rat is the most sensible these days. The toad was the funniest, though.\n\n[half]\f[italic,true]That seemed to go over well."
		--
		snakeFeelings += 7
	}
	if snakeFeelings >= 50
	{
		"Jack London's short story Bâtard.\f[italic,default]\n\n[half]\0\s[8]Read this one a lot after leaving the army.\w8\w8\w8\w8\w8 I got over it.\n\n[half]\f[italic,true]Snake stows the thin book carefully into a bag. He doesn't seem unhappy, though."
		snakeFeelings += 10
	}
	--
	"\e"

}
//---- Snake Apologies ---------------------------------------------------------
//Apologizing to Snake. only appears if feelings are lower than 50. This is to help users get back onto a positive track, should they bring his feelings down considerably, but it's not an easy short cut. it's still quite negative. Snake's largest bonus, Cigarettes, would still need to be given to him six times in a row to 'fix' it from this point.

Select.sayApology
{
	"\0\s[8]\f[italic,true]You apologize to Snake for the various things you've done to upset him.\n\n\f[italic,default]\0\s[8]... "
	--
	if snakeApologies >= 3
	{
		"This is the last time. Don't fuck with me again. "
	}
	--
	"\w8\w8Make sure your actions speak for your words."
	"\w8\w8Make it count."
	"\w8\w8Fine. Don't think I'll forget what you did, though."
	--
	"\n\n\f[italic,true]Snake has forgiven you. Partially.\f[italic,default]\e"
	snakeFeelings = -30
	--
	if beginnerMode == "On" //if the user somehow activates this function while beginner mode is On, their apologies will be reset.
	{
		snakeApologies = 0
	}
	else
	{
		snakeApologies++
	}
	
}
//---- Snake FAQ ---------------------------------------------------------
//Asks Snake about the Shadow Moses Incident. Answer depends on feelings.

Select.askShadowMosesIncident
{
	if snakeFeelings >= 50
	{
		"\0\s[9]I was assigned to infiltrate Shadow Moses Island during the Shadow Moses Incident. Otacon and I met on Shadow Moses.\0\s[4] My old unit, FOXHOUND, came under the command of \_a[whoLiquid]Liquid Snake\_a, another clone of Big Boss, and took over Shadow Moses. They also captured Metal Gear REX, Otacon's project.\0\s[0] I had to kill each of the elite members: Decoy Octopus, Psycho Mantis, Sniper Wolf, Vulcan Raven,\0\s[4] and... Liquid. The other member, Ocelot, escaped with his life. With the help of an old friend, we managed to destroy REX.\0\s[0] It all came at a heavy price, but I don't regret it.\e"
		clickShadowMosesIncident++
	}
	elseif snakeFeelings >= 20 && snakeFeelings <= 49
	{
		"\0\s[4]The Shadow Moses Incident was the terrorist take-over of Shadow Moses Island by \_a[whoLiquid]Liquid Snake\_a.\f[size,8] We're related, in a way.\f[size,default]\0\s[0] He and the rest of the terrorist group, FOXHOUND, captured the base for the Metal Gear inside. Eventually, under the guidance of Colonel Campbell, my former commander, the government was successful in taking them down."
		clickShadowMosesIncident++
	}
	elseif snakeFeelings >= -29 && snakeFeelings < 20
	{	
		"\0\s[8]Hmm...\n\n\f[italic,true]Snake huffs, and shifts his eyes side to side. It seems he won't talk about this right now.\f[italic,default]\e"
	}
	elseif snakeFeelings <= -30 && snakeFeelings >= -49
	{
		"\0\s[8]\f[italic,true]Snake glares at you. His hair prickles slightly. Oops.\f[italic,default]\e"
	}
	elseif snakeFeelings <= -50 && snakeFeelings >= -89
	{
		"\0\s[7]Why the hell would I tell someone like you?\1\s[13]\0\s[7]\n\n\f[italic,true]It's clear Snake didn't like that.\f[italic,default]\e"
		snakeFeelings--
	}
	else
	{
		"\0\s[9]\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\0\s[0]\w8\w8\w8Fuck you.\e"
		snakeFeelings -= 5
	}
}

Select.askLiquidSnake
{
	"\0\s[8]Liquid Snake was the leader of FOXHOUND in 2005. "
	--
	if snakeFeelings <= 19
	{
		"He was a bit like you, thinking about it."
	}
	--
	if snakeFeelings > 20
	{
		"He was considered a terrorist because he took over Shadow Moses Island by force on Febuary 25th, 205. He made a big show out of demanding the body of Big Boss or he would steal \_a[whoRex]Metal Gear REX\_a, which was being kept on the island. "
	}
	--
	if snakeFeelings > 30
	{
		"Metal Gear REX was destroyed before his demands could be met. "
	}
	--
	if snakeFeelings > 40
	{
		"\n\n[half]I took down Metal Gear Rex myself. Liquid and I fought hand-to-hand on top of the remains of REX. He told me if I fell, the height of the drop would kill even me. I knocked him off of REX and made my escape, just in time before an airstrike was supposed to hit the base. "
	}
	--
	if snakeFeelings > 50
	{
		"\n\n[half]He and I were cloned from Big Boss. Big Boss was the commander of FOXHOUND when I was a rookie, and a folk hero to most of North America. A legendary soldier. "
	}
	--
	if snakeFeelings > 60
	{
		"\n\n[half]I don't consider Liquid a brother, I didn't know of his existence or our cloning until Shadow Moses.\w8\w8 I don't take a lot of importance in blood relations. Maybe it comes from being handed from family to family as a kid. "
	}
	--
	if snakeFeelings > 80
	{
		"\w8\w8\w8\n\n[half]\0\s[0]After my confrontation with him, Liquid died. He survived the fall, but I had been carrying a virus called FOXDIE which recognized specific people's DNA. Those people were killed by the virus inducing cardiac arrest. FOXDIE was programmed to kill me at a random time in the future. It caught Liquid in the crossfire due to our genetic similarity. "
	}
	--
	if snakeFeelings > 90
	{
		"\w8\w8\n\n[half]\0\s[0]I don't think about him much anymore. I've come to terms with being a clone, and I didn't know him for long.\0\s[6] He's another name on the long list of people who died directly or indirectly because of me."
	}
}

//Asks Snake about his love life. Most of this is headcanon, because Snake's early life doesn't have a lot known about it.
Select.askLove
{
	if askLove == 0
	{
		"\0\s[8]Love life? Are you kidding me?\w8\w8\w8.\w8.\w8 You're not joking? Hmmh. Let me think."
		askLove = 1
	}
	elseif askLove == 1
	{
		"\0\s[0]You're serious. Fine. I flirted with a couple girls, dated some for a little while. That's it." 
		askLove = 2
	}
	elseif askLove == 2 && snakeFeelings < 60
	{
		"\f[italic,true]Snake narrows his eyes. Seems like now is not the time."
	}
	elseif askLove == 2 && snakeFeelings >= 60
	{
		"\0\s[0]Used to know this girl. Holly. She invited me to Christmas Dinner, but I didn't stick around. Too young for me, anyway."
		askLove = 3
	}
	elseif askLove == 3 && snakeFeelings < 70
	{
		"\0\s[0]\f[italic,true]Snake rolls his eyes and seems annoyed. Seems like now is not the time."
	}
	elseif askLove == 3  && snakeFeelings >= 70
	{
		"\0\s[6]You're still on this?\w8.\w8.\w8 \nIf you remember Holly, I said she was too young for me. Meryl was also young. Never saw me for who I was. She wanted 'Snake'. Always called me that, no matter how many times I told her she could call me David. Didn't take me long to find out the reason she was so stubborn. She was only eighteen. Being my commander's neice, I thought she was older. I skipped town after her. I don't think girls are worth the trouble anymore."
		askLove = 4
	}
	elseif askLove == 4
	{
		"\0\s[0]Mmm. Okay. You're still not satisfied? Otacon already knows, but I'll humor you. \nBack when I first retired, after Outer Heaven, I wasn't what most people call 'stable'. Most veterans experience all kinds of instability after retiring. Me? I slept around. I flirted with anything on two legs that moved and wanted me, even if I wasn't interested. Let girls use me. It numbed the pain while I licked my wounds. Same thing happened after Zanzibar. The only thing that got me to quit was living twenty miles away from anybody alive and drinking myself to death in Alaska. Happy? You got what you asked for. That's it. Don't ask me about it again."
		askLove = 5
	}
	elseif askLove == 5
	{
		"\0\s[0]What? I've told you all there is about it. "
		--
		if otaconFeelings >= 70
		{
			askLove = 6 //I think this is the first time I've put something which permanently diverges, besides limiting Snake's apologies. Feels cool.
			"\0\s[0]\n\n[half]\f[italic,true]You point out Otacon.\f[italic,false]\n\n[half]\0\s[6]Otacon..?\1\s[30]\w8\w8\w8\w8\0\s[0] Mhmm.\n\n[half]\f[italic,true]Snake looks at you for a moment, as if weighing his options.\w8\w8\w8\f[italic,false]\n\n[half]I watch is back and he watches mine. He's my partner. For us, it means multiple things. One thing that doesn't change is I'll always be there to protect him.\n\n[half]\f[italic,true]\w8\w8You say he's dodging the question.\f[italic,false]\n\n[half]\w8\w8I don't recall you asking one.\1\s[32]\e"
		}
		else
		{
			askLove = 7
			"\0\s[8]\n\n[half]\f[italic,true]You ask Snake about Alaska.\f[italic,false]\n\n[half]Mm. That's where I moved after Zanzibar. Nice place called Twin Lakes. I dealt with a lot of thoughts. Memories.\w8\w8 Got a bad habit of drinking to drown out the flashbacks. Otacon helped me drop that one.\1\s[14]\0\s[8] Nearly tore us apart, though.\s[9]\w8\w8\f[size,8] The drinking didn't help with Meryl."
		}
	}
	elseif askLove == 6 || askLove == 7
	{
		"\f[italic,true]You've learned all you can about this part of Snake. However, you can go back and listen to what he told you again, if you forgot. Which part would you like to return to?\f[italic,default]\n"
		--
		"\q[Flirting,loveFlirting]\n/
		\q[Holly,loveHolly]\n/
		\q[Meryl,loveMeryl]\n/
		\q[Instability,loveInstability]\n"
		--
		if askLove == 6
		{
			"\q[Otacon,loveOtacon]"
		}
		elseif askLove == 7
		{
			"\q[Alaska,loveAlaska]"
		}
	}
}

Select.loveFlirting //I'll do these in only two lines so they don't take up a huge amount of space.
{"\0\s[0]You're serious. Fine. I flirted with a couple girls, dated some for a little while. That's it."}
Select.loveHolly
{"\0\s[0]Used to know this girl. Holly. She invited me to Christmas Dinner, but I didn't stick around. Too young for me, anyway."}
Select.loveMeryl
{"\0\s[6]You're still on this?\w8.\w8.\w8 \nIf you remember Holly, I said she was too young for me. Meryl was also a young one. Never saw me for who I was. She wanted 'Snake'. Always called me that, no matter how many times I told her she could call me David. Didn't take me long to find out the reason she was so stubborn. She was only eighteen. Being my commander's neice, I thought she was older. I skipped town after her. I don't think girls are worth the trouble anymore."}
Select.loveInstability
{"\0\s[0]Mmm. Okay. You're still not satisfied? Otacon already knows, but I'll humor you. \nBack when I first retired, after Outer Heaven, I wasn't what most people call 'stable'. Most veterans experience all kinds of instability after retiring. Me? I slept around. I flirted with anything on two legs that moved and wanted me, even if I wasn't interested. Let girls use me. It numbed the pain while I licked my wounds. Same thing happened after Zanzibar. The only thing that got me to quit was living twenty miles away from anybody alive and drinking myself to death in Alaska. Happy? You got what you asked for. That's it. Don't ask me about it again."}
Select.loveOtacon
{"\0\s[0]What? I've told you all there is about it. \n\n[half]\f[italic,true]You point out Otacon.\f[italic,false]\n\n[half]\0\s[6]Otacon..?\1\s[30]\w8\w8\w8\w8\0\s[0] Mhmm.\n\n[half]\f[italic,true]Snake looks at you for a moment, as if weighing his options.\w8\w8\w8\f[italic,false]\n\n[half]He's my partner. For us, it means multiple things. One thing that doesn't change is I'll always be there to protect him.\n\n[half]\f[italic,true]\w8\w8You say he's dodging the question.\f[italic,false]\n\n[half]\w8\w8I don't remember you asking one.\1\s[32]\e"}
Select.loveAlaska
{"\0\s[0]What? I've told you all there is about it. \0\s[8]\n\n[half]\f[italic,true]You ask Snake about Alaska.\f[italic,false]\n\n[half]Mm. That's where I moved after Zanzibar. Nice place called Twin Lakes. I dealt with a lot of thoughts. Memories. Got a bad habit of drinking to drown out the flashbacks. Otacon helped me drop that one. Nearly tore us apart, though. The drinking didn't help with Meryl."}

//Asks Snake about Frank Jaeger, Gray Fox.
Select.askJaeger
{
	if snakeFeelings >= 35
	{
		"\0\s[8]Gray Fox. That was his codename. He and I were close. "
		if snakeFeelings <= 19
		{
			"That's all you need to know."
		}
		--
		if snakeFeelings > 20
		{
			"We were in FOXHOUND together. He noticed me when I was a rookie. "
		}
		--
		if snakeFeelings > 30
		{
			"Jaeger was the first and only soldier in the unit to achieve the highest code name. 'Fox'. "
		}
		--
		if snakeFeelings > 40
		{
			"Gray Fox taking notice of me was surprising. Gave me a big head. "
		}
		--
		if snakeFeelings > 50
		{
			"\w8\w8During \_a[whoN312]N312\_a, we recieved he was captured. I knew I had to do something. Back then, I thought Big Boss picked me for \_a[whoN313]N313\_a for the same reason. I was too green for the operation and Boss knew it. \1\s[18]"
		}
		--
		if snakeFeelings > 60
		{
			"\1\s[18]\w8\w8...You loved him. Frank.\0\s[4]\n\n[half]I'd have done anything to save him. "
		}
		--
		if snakeFeelings > 80
		{
			"\0\s[0]\nHis loyalty to Big Boss came in conflict with me. We came to blows in Zanzibar, and we fought one last spar.\0\s[6] He was still damn quick...\w8\w8\w8 I thought he didn't make it out of Zanzibar alive. "
		}
		--
		if snakeFeelings > 90
		{
			"\0\s[9]When we met again at Shadow Moses, he was unrecognizable. FOXHOUND took his body away from him. Shoved him in an exoskeleton, stripped him of his humanity and used his body for research on gene therapy. There was no respect for death. "
		}
		--
		if snakeFeelings > 100
		{
			"\0\s[0]In the end, his goal was still the same. Despite everything, he wanted one last spar with me. Put things back the way they were intended.\w8\w8\w8 Gray Fox was killed by Liquid Snake. For the final time."
		}
		--
		"\n\n"
		--
		if snakeFeelings < 60
		{
			"\f[italic,true]You have a feeling there's still more to Gray Fox."
		}
		--
		"\e"
	}
	else
	{
		"\0\s[8]\f[italic,true]Snake studies you warily. You have a feeling Snake doesn't want to talk about who Jaeger is right now.\e"
	}
}

//Asks Snake about Big Boss. Currently unimplemented.
Select.askBigBoss
{
	"\0\s[0]Big Boss was the founder and commander of FOXHOUND in the early 1970s and throughout the 1990s. He was a legendary soldier and well-known in America, making the covers of magazines and at least a handful of books. Big name, big title. "
	--
	if snakeFeelings <= 19
	{
		"\0\s[8]Surely you should know this. "
	}
	--
	if snakeFeelings > 20
	{
		"\0\s[0]In the 1990s, he secretly developed a large military force known as Outer Heaven occupying Galzburg in South Africa. He also later went on in 1997 to participate in the Mercenary War and managed to scrape together the independence of a province called Zanzibar. "
	}
	--
	if snakeFeelings > 30
	{
		"\n\n[half]Outer Heaven was later known to be developing a Metal Gear in 1995. A category of robot which is nuclear armed and bipedal. Zanzibar Land was also found to be building a Metal Gear in its own time too. "
	}
	--
	if snakeFeelings > 40
	{
		"\n\n[half]Now, in 1995, Big Boss hadn't been revealed to be behind Outer Heaven yet. He was still the commander of FOXHOUND. As a cover, he sent Gray Fox to Outer Heaven to destroy the Metal Gear. That was Operation Intrude N312. Fox never made it back, so Big Boss sent me. That operation was called Intrude N313. I sucessfully destroyed the Metal Gear. "
	}
	--
	if snakeFeelings > 50
	{
		"\n\n[half]Big Boss wasn't particularly happy I'd destroyed his pet project when N313 was supposed to be a suicide mission. He tried to stop me himself,\w8 supposedly anyway. He had a body double of some kind, who I killed fully believing he was Big Boss. "
	}
	--
	if snakeFeelings > 60
	{
		"\0\s[8]\n\n[half]I couldn't stand it. Big Boss and I had become somewhat close in FOXHOUND. He taught me CQC- Close Quarters Combat- a fighting technique he had created. He gave me my codename, one I still use. He even told me he was my father. "
	}
	--
	if snakeFeelings > 80
	{
		"\0\s[6]\n\n[half]I quit FOXHOUND after that. I was brought back to destroy the Metal Gear in Zanzibar, and I fought Big Boss once more. The real Big Boss. I killed him with my own hands. He burned to death. Slowly and painfully, screaming. Screaming my name. "
	}
	--
	if snakeFeelings > 90
	{
		"\n\0\s[8]After that I went to live in Alaska and kept myself black-out drunk as much as possible. They say alcohol is good at drowning out the screaming."
	}
}
Select.askOtacon
{
	if snakeFeelings >= 20
	{
		"\0\s[8]Otacon, huh? He's my partner, I met him on Shadow Moses. We started an NGO called Philanthropy together with a few others to prevent and destroy derivates of \_a[whoRex]Metal Gear Rex\_a. Otacon gathers all the intel and I do the dirty work. He's white collar. I'm blue collar. Sometimes this gets him into trouble, diggng places he shouldn't be. I do my best to protect him whenever possible. "
		--
		if askLove == 6
		{
			"We fool around sometimes. He's real shy about it, though. So don't go screaming 'ho yay'. "
		}
	}
	elseif snakeFeelings >= -10 && snakeFeelings < 20
	{
		"\0\s[8]Otacon? He's my partner. We work together on a personal project. It's makes us move around a lot. I've seen him geek out about being in here already, so I don't think he minds the extra stop. I'd prefer to get going as soon as possible."
	}
	else
	{
		"\0\s[8]Who's Otacon? Ask him yourself. He's right here."
	}
	--
	"\n\n[half]\0\s[0]\f[italic,true]You ask him how Otacon is doing.\f[italic,default]\n\n[half]"
	--
	if snakeFeelings <= -20
	{
		"\0\s[8]Why are you asking me?"
	}
	elseif otaconFeelings <= -50
	{
		"\0\s[8]\w8.\w8.\w8. Leave him alone. Got it?"
	}
	elseif otaconFeelings > -50 && otaconFeelings <= -20
	{
		"\0\s[8]He's been rather forgiving with you."
		--
		if snakeApologies = 3
		{
			" So have I."
		}
		--
		"So don't mess anything else up. Got it?"
	}
	elseif otaconFeelings > -20 && otaconFeelings <= 10
	{
		"\0\s[8]He's doing fine. Keep it that way."
	}
	elseif otaconFeelings > 10 && otaconFeelings <= 20
	{
		"\0\s[8]He seems happy. Is that good enough for you?"
	}
	elseif otaconFeelings > 20
	{
		"\0\s[0]He likes it here. A little cheerier than usual. It's good for him."
		--
		if snakeFeelings > 30
		{
			"\0\s[9] It gets a little isolated with just him and me. Nastasha and the others just aren't the same. Talking to you seems to have put some life back into him, hm?"
		}
		--
		if snakeFeelings > 50 && otaconFeelings > 60
		{
			"\0\s[4]\w8.\w8. He had a hard life, you know. He's done well for himself since."
		}
	}
	--
	"\e"
}
//If someone selects an option linked to BACKTOMENU, it will go back to MenuRun.
//Advanced users, note that this is formatted as Select.linkedfunction? Remember how it went \q[Displayed Name,linkedfunction] above? When a user clicks an option that doesn't begin with On, they'll be linked to Select.linkedfunction. Pay close attention as we go through and you'll see this in action.

Select.BACKTOMENU
	{
		MenuRun
	}

//If someone selects configmenu, this will lead to TalkConfig.
Select.configmenu
	{
		TalkConfig
	}

//--TalkConfig
//This menu allows the user to change their name and other information, as well as put in their birthday and change how often the ghost speaks. You can restructure how this looks if you want, but it's perfectly functional as is. You can of course change the dialogue though if you like.

TalkConfig
{
	"\0\b[2]\_q\" 
	//This bit of dialogue sets up a large balloon and a quick session, like above, so all the text will appear immediately. The rest is formatted to display the information and options to change it. I wouldn't touch any of it if you don't know what you're doing.
	//I'll briefly explain the font tags I used here. \f[bold,1] turns bold on, \f[bold,0] turns it off, not unlike a <b> tag. \f[sup,1] turns superscript on (making it tiny and above the text) and \f[sup,0] turns it off. I touch on these tags in the walkthrough on SakuraScript.
	--
	"f[bold,1]Your name:\f[bold,0] %(username) \f[sup,1](\q[Change it?,TEACHNAME])\f[sup,0]\n"
	--
	"\f[bold,1]Your pronouns:\f[bold,0] %(heshe)/%(himher)/%(hisher) \f[sup,1](\q[Change them?,TEACHPRONOUN])\f[sup,0]\n"
	--
	"\f[bold,1]Your birthday:\f[bold,0] %(birthdayprint) \f[sup,1](\q[Change it?,TEACHBDAY])\f[sup,0]\n\n"
	--
	"\f[bold,1]Talk Rate:\f[bold,0] every %(talktime) \f[sup,1](\q[Change it,talkinter])\f[sup,0]\n\n"
	--
	"\f[bold,1]Beginner Mode:\f[bold,0] %(beginnerMode) \f[sup,1](\q[Change it,beginnerYN])\f[sup,0]\n\n"
	--
	"\f[bold,1]Punch Config:\f[bold,0] %(punchConfig) \f[sup,1](\q[Change it,punchYN])\f[sup,0]\n\n"
	--		
		{
		"\![*]Enable Uninstall? (NOW : %(deleteshitai))\n\q[ON,deleteon] \q[OFF,deleteoff]\n\n[half]\n"
		}
	--
	"\![*]\q[Back to the menu,BACKTOMENU]\e"
}

//This and the next few set up the pronouns, as you can see by the linkedfunction names. You can replace "\0\s[0]What pronouns do you want?" with your own dialogue if you prefer, but leave the rest of it alone.

Select.TEACHPRONOUN
{
	"\0\s[0]What pronouns do you want?\w5\n\n[half]\_q/
		\![*]\q[He-His-Him,choicemasc]\n/
		\![*]\q[She-Her-Her,choicefem]\n/	
		\![*]\q[They-Their-Them,choiceneither]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	himher = "him"
	heshe = "he"
	hisher = "his"
	hesshes = "he's"
	--
	"\0\s[0]He and him, huh? Got it.\e"
	//Leave the part above the double dashes alone. You can replace the snip of dialogue however with whatever you like.
}

Select.choicefem
{
	presuffix = "feminine"
	himher = "her"
	heshe = "she"
	hisher = "her"
	hesshes = "she's"
	--
	"\0\s[0]She and her, huh? Got it.\e"
	//as above, you can also replace this dialogue.
}

Select.choiceneither
{
	presuffix = "undefine"
	himher = "them"
	heshe = "they"
	hisher = "their"
	hesshes = "they're"
	--
	"\0\s[0]They and them, huh? Got it.\e"
	//and this one.
}

//Here are some examples of these envelopes in use.
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."


//These two options toggle whether or not you can delete your ghost. Leave them alone.

Select.deleteon
{
	deleteshitai = "ON"
	TalkConfig
}

Select.deleteoff
{
	deleteshitai = "OFF"
	TalkConfig
}


//This next set of functions handles inputting the user's birthday.

Select.TEACHBDAY
	{	
		"\0\s[0]What month were you born? \1\s[10]\w8Name or number is fine.\0\s[0]\![open,inputbox,OnUserBornMonth,-1]\e"
		
		//You can replace the dialogue up to the \![open,inputbox,OnUserBornMonth,-1] tag, leave that alone. You'll need the input box to get the information.
		
		//*********************Advanced User Info*******************************
		//I also touched on this in bootend.dic and explained it in the walkthrough page on coding, but I'll also go into it here. I'm going to break down the tag there at the end of the sentence so you know what it's doing. Basically, \! means a function is going to happen, open tells it to open something, inputbox tells it to open an input box, and OnUserBornMonth is the function the input box will be linked to, which is just below here. -1 means the input box should not time out. If you want to add input boxes to some new function for whatever reason, you should follow this basic format.
		//On a related note, you can raise any function at the end of any piece of dialogue by using this format - \![raise,InsertFunctionName]. This can be handy for testing things if the function is hard to get to run otherwise.
		//***********************************************************************
	}

//--OnUserBornMonth
//This takes the value that the user put into the input box above and evaluates it. Most of this you won't have to touch, but I'll explain what it's doing anyway. I'll point out where you can change the dialogue.

OnUserBornMonth
{
	_tempmonth = TOSTR(reference0) //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	//this is attaching the right value to the input from the user
	if _tempmonth == "january" || _tempmonth == "1" {bornmonth = 1; bornmonthprint = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {bornmonth = 2; bornmonthprint = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {bornmonth = 3; bornmonthprint = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {bornmonth = 4; bornmonthprint = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {bornmonth = 5; bornmonthprint = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {bornmonth = 6; bornmonthprint = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {bornmonth = 7; bornmonthprint = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {bornmonth = 8; bornmonthprint = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {bornmonth = 9; bornmonthprint = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {bornmonth = 10; bornmonthprint = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {bornmonth = 11; bornmonthprint = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {bornmonth = 12; bornmonthprint = "December"}
	
	else //if the month isn't a valid month, like 32 or something.
	{
			"\1\s[18]Err, that's not a valid month. Try again, please. If it's a single digit month, don't write it with a zero in front, okay?"
			--
			"\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
			
			//You can replace this dialogue in the first line. You need to leave the tag for the input box so they can put in a correct month. That last tag there for ![set,balloontimeout,-1] makes it so the input box won't time out if the user takes too long.
	}
	--
	//If it's a valid month
	
	"\0\s[0]So,\w8 you were born in %(bornmonthprint), huh?\n\w8What day was it?"
	--
	"\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\e"
	
	//Here you can see the use of a bornmonthprint envelope to show the user what month they put in. Notice that it opens an inputbox at the end the same way the one above did. You can replace the dialogue in the first line just as with above.
}

//This adds an appropriate suffix to the number for the day. No need to mess with this.

Printday
{
	if bornday == 1 || bornday == 21 || bornday == 31
	{	"%(bornday)st"	}
	elseif bornday == 2 || bornday == 22
	{	"%(bornday)nd"	}
	elseif bornday == 3 || bornday == 23
	{	"%(bornday)rd"	}
	else
	{	"%(bornday)th"	}
}	

//--OnUserBornDay
//This is much like the above function OnUserBornMonth. It'll take the value the user put in for the day and analyze it. Again, there'll be comments explaining how it works, but all you really need to do is change the dialogue appropriately. I'll point out where you can add new dates to check for.

OnUserBornDay
{
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	
	if ((bornmonth == 1 || bornmonth == 3 || bornmonth == 5 || bornmonth == 7 || bornmonth == 8 || bornmonth == 10 || bornmonth == 12) && _tempday >= 1 && _tempday <= 31) || (bornmonth == 2 && _tempday >= 1 && _tempday <= 29) || ((bornmonth == 4 || bornmonth == 6 || bornmonth == 9 || bornmonth == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range. If it does, we set some variables and continue down.
	{
		bornday = _tempday
		borndayprint = Printday
		userbirthday = "%(bornmonth)month %(bornday)day"
		birthdayprint = "%(bornmonthprint) %(borndayprint)"
		
		"%(birthchecked)"
		//Mostly defining envelopes and values in here. No need to touch any of this! Eagle-eyed readers will notice the new envelope birthchecked on its own line like a tiny king. We'll get to that momentarily, but pay it no mind for now.
	}
	else //If the day isn't valid, like the 10046th or something, this will open the input box again and have a \e so the dialogue below doesn't play. You can replace the dialogue in the first line here.
	{
		"\1\s[18]That's not a valid day. Try again, please."
		--
		"\![open,inputbox,OnUserBornDay,-1]\e"
	}	
}

//--birthchecked
//This is the new envelope you saw me point out above. As you can see here, you can actually call dialogue in a function with an envelope, even when it has if and else statements like here! Which can be pretty handy. Again, more on envelopes in word.dic. Creative ghostmakers can find handy ways to exploit this, I'm sure.

birthchecked
{
	if userbirthday == dayslot //if today is the user's birthday
		{
			"\1\s[15]It's the user's birthday!\w8 Happy birthday, %(username)!\e"
			//replace this dialogue with whatever you like
		}
	elseif userbirthday == "12month 25day"
		{
			"\0\s[0]Born on Christmas, huh?\w8\1\s[15]Too bad you have to share it with the same day you get given a bunch of gifts anyway. \w5\w8\0\s[9]\n\n[half] Wait, you're getting gifts?\e"
			//As you can see, you can set up an elseif statement to check if the user's birthday is any day you want! More on elseif statements in bootend.dic. In this case, there's specialized dialogue if the user was born on Christmas. You can replace this with whatever you like, or change the date you're checking as well, like to one of your character's birthdays if you want. You can also delete this elseif if you don't care.
		}
	else //if the birthday is not listed above.
		{
			"\0\s[0]Your birthday is %(bornmonthprint) %(borndayprint), hm? We'll remember.\e"
			//note the use of envelopes to display the newly set birthday properly. You can replace this dialogue as well, and you can use %(bornmonthprint) and %(borndayprint) to display the month and day in the new dialogue, if you want.
		}
}

//--Talk Rate
//This is when the user chooses to adjust the talk rate in the config menu. The talk rate is how often the ghost will read some dialogue from aitalk.dic.

Select.talkinter
 	{
 		"\0\s[0]\b2How often should we speak?\w8\1\s[10]The interval is currently set to %(talktime)./
		//note the new envelope here for %(talktime). You'll see it again in a second. You can replace the dialogue above with whatever you like, just make sure it ends with ./ and not \e.
 		\0\n\n[half]/
 		\![*]\q[Every 30 seconds,TALKMAX]\n/
		\![*]\q[Every minute,TALKHIGH]\n/
 		\![*]\q[Every 3 minutes,TALKNORMAL]\n/
		\![*]\q[Every 5 minutes,TALKPOOR]\n/
 		\![*]\q[Don't speak,TALKNONE]\n\n[half]/
		\![*]\q[Don't change,TalksTill]\e"
	}

//If the user doesn't want to change the rate. You can change all of the dialogue in any of these options if you want to reflect the choice the user made. You TECHNICALLY could also change the value in talktime to reflect some other way of measuring time but I wouldn't recommend it, it'd just be confusing I'm sure.
Select.TalksTill
	{
		"\0\s[0]No change? Fine by me.\e"
		"\0\s[0]Change your mind?\e"
		"\0\s[0]Nothing? Alright."
		"\1\s[10]Do you like the way we are now?"
	}

//If the user chooses every 30 seconds.
Select.TALKMAX
	{
 		talktime = "30 seconds" //this sets up the envelope properly
		aitalkinterval = 30 //this changes the actual rate
		
 		"\0\s[0]We'll speak every 30 seconds.\1\s[15]Like us talkative, huh?\0\s[2]\n\n[half]I like talking with you, too, Otacon.\1\s[30]\n\n[half]Hah?\e"
	}

//If the user chooses every minute.
Select.TALKHIGH
	{
 		talktime = "minute"
		aitalkinterval = 60
		
 		"\0\s[0]We'll speak every minute, got it.\e"
		"\1\s[10]Every minute? Okay! I hope you have something to talk about, Snake.\e"
		"\0\s[0]Every minute, huh? Got it."
	}

//If the user chooses every three minutes.
Select.TALKNORMAL
	{
 		talktime = "3 minutes"
		aitalkinterval = 180
		
 		"\0\s[0]We'll speak every three minutes.\e"
	}

//If the user chooses every five minutes. This is the default rate the ghost will start up in, as seen in bootend.dic.
Select.TALKPOOR
	{
 		talktime = "5 minutes"
		aitalkinterval = 300
		
 		"\1\s[10]Okay, every five minutes. Hear that, Snake?\0\s[8]I'm not old yet, Otacon.\e"
		"\1\s[10]Alright, we'll speak every five minutes.\e"
		"\0\s[0]Speak every five minutes. Got it."
	}

//If the user chooses to not have them speak. This is one case where you MIGHT want to make talktime equal something else, if you have a snappier way of saying the ghost won't talk. Make sure it flows well in your menus though, since it will be displayed in your config menu.
Select.TALKNONE
	{
 		talktime = "N/A"
		aitalkinterval = 0
		
		"\0\s[0]We won't speak.\1\s[15]Am I that annoying? Hah.\e"
		"\0\s[0]No talking, huh? Got it."
	}

	
//This is when the user chooses to change their name. It's linked to the functions outlined in nameteach.dic. Nothing to touch here.
Select.TEACHNAME
	{
		OnTeachName	
	}


Select.beginnerYN //Toggle Beginner Mode
	{
		"\0\s[8]Alright, do you want beginner mode on or off? Beginner mode disables and softens some negative repercussions when enabled. When disabled, we may be less forgiving to your actions./
		\![*]\q[Turn On,beginnerStart]\n/
		\![*]\q[Turn Off,beginnerEnd]\n"
		//If you're lost on where beginnerStart and End are, they are in Sn_nameteach.dic.
	}

Select.punchYN //Toggle Punch Functions
{
	"\0\s[8]Punch Config. This enables certain punching modes. / 
	\nWhen set to On, you'll automatically punch us when you double click areas of interest. /
	\nWhen set to Off, you won't be able to punch us at all, and simply point when you double click. /
	\nWhen set to Confirm, you will be asked to confirm the punch before it happens. /
	\nWhen set to Apologize, you will automatically punch when double clicking, but have the option to apologize. Which will it be?\n/
	\![*]\q[On,punchOn]\n/
	\![*]\q[Off,punchOff]\n/
	\![*]\q[Confirm,punchConfirm]\n/
	\![*]\q[Apologize,punchApologize]"
}
Select.punchOn
{
	punchConfig = "On"
	"\0\s[0]Alright. I'll roll with the punches.\1\s[17]What am I supposed to do?\0\s[8]\n\n[half]I won't let %(username) hurt you.\w8\w8\1\s[14]\n\n[half]Okay. I trust you."
}
Select.punchOff
{
	punchConfig = "Off"
	"\0\s[0]Alright. I'll take any double clicks as pointing."
}
Select.punchConfirm
{
	punchConfig = "Confirm"
	"\0\s[0]Alright. It will ask you to confirm your punches before actually doing so."
}
Select.punchApologize
{
	punchConfig = "Apologize"
	"\0\s[0]Alright. I'll give you a chance to apologize after hitting me.\1\s[17]Me too, even if I hope you don't at all..."
}
//If the user selects cancel in a menu. Replace as you like, or just leave them blank.
Select.CANCEL
	{
		"\0\s[0]All done, huh?\e"
		"\0\s[0]Change your mind?\e"
		"\0\s[0]That all?"
		"\0\s[0]Mhm."
		"\0\s[0]Mm-hmm."
		""
 	}

	
//Going back to the MenuRun function we finished above, this is if someone selects the Functions option, linked to dosomething. As you can see, it ends up as Select.dosomething.

Select.dosomething
{
 		"\0\s[0]\b2This is the functions menu, hm?\n\n"
		"\0\s[0]\b2Functions menu, just for you.\n\n"
		"\0\s[0]\b2Got it. Functions menu.\n\n"
		
		//I mentioned this above in MenuRun, but you can have a set of multiple different responses that may randomly appear when the menu is opened if you offset them from the rest with the -- dashes below this note. Try opening this menu yourself with the template to see how it works. The character will randomly choose one line as the intro. 
		//If you don't want to deal with this, just delete the extra lines and leave one. Otherwise, replace the dialogue above as you like, but note that they don't end with \e here. You can leave the rest of this below alone.
		--
 		"\![*]\q[Open Calendar,CALENDAR]\n/
		\![*]\q[Check Email,MailCheck]\n/
		\![*]\q[Open SSP Preferences,sspconfig]\n/
		\![*]\q[Empty Recycle Bin,ChoiceGarbagecan]\n/
		\![*]\q[System Information,ChoiceSystemInformation]\n\n/
 		\![*]\q[Back to main menu,BACKTOMENU]\e"
}

//When the user clicks Open Calender. This will open the calender function of SSP, an in-built feature that you don't have to worry about in the least.
Select.CALENDAR
{
	"\0\s[0]Got it. I'll get the calendar.\w8\![open,calendar]\e"
	
	//You can replace the dialogue here up to the \!.
	//Advanced users, can you break it down yet? It's basically just using \! to open calender.exe or dll or whatever SSP uses to make it happen.
}

//When the user chooses SSP preferences. This will open the same window that shows up if you right click your ghost and go to Utilities - Preferences.
Select.sspconfig
{
	"\0\s[0]Got it. SSP Config Menu.\w8\![open,configurationdialog]\e"
	
	//Again, you can replace up to the \! tag.
}

//If the user chooses to empty the recycle bin.
Select.ChoiceGarbagecan
{
	_gomicheck = FUNCTIONEX("gomi.dll", "-n")	//Don't touch this line. for those curious, it's referencing a SAORI, which are optional user-made plug-in dlls you can download to enable different functions on your ghost. gomi.dll is one included with this template. All SAORI are in Japanese (maybe Korean or Chinese as well idk) and many have disappeared by now, so don't concern yourself very much with SAORI. They're for skilled developers, mostly.
	
	if _gomicheck == 0 //If the recycle bin is empty.
	{
		"\0\s[0]Otacon, go check the recycle bin.\w8\1\s[19]Fine, Snake. You know you could do it sometimes, too.\w8\w8\s[-1]\w8\w8\w8\w8\w8\1\s[10]\cIt was empty. Happy?\e"
		
		//This isn't the usual flat template dialogue, mostly because I wanted to show that this is a good place to use s[-1], which I covered in the walkthrough file. You can have your ghost disappear momentarily like they're going to check your recycle bin, then come back. \c clears the ghost's balloon of text. Replace dialogue as you desire.
	}
	elseif _gomicheck >= 1 //If the recycle bin is full.
	{
		"\0\s[0]Empty the recycle bin. Got it.\0\s[20]\w2\0\s[21]\w2\0\s[22]\w1\0\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\1\s[10]\cMission accomplished, come back to base, Snake.\w8\w8\w8\0\s[22]\w2\0\s[21]\w2\0\s[20]\w1\0\s[0]\e"
		
		"\0\s[0]Otacon, you empty the recycle bin, this time.\w8\1\s[19]Fiiine.\w8\w8\s[-1]\w8\w8\w8\w8\w8\![raise,OnGarbagecanEmpty]\w9\w9\w9\1\s[10]\cI did it. I saw some nice files in there, though...\e"
		
		//Somewhat similar to the above, but notice the \![raise],OnGarbagecanEmpty] tag in there. That's what actually empties the recycle bin, so make sure you don't delete that tag. Otherwise, replace the dialogue as you want.
		//As you can see, you can have multiple pieces of dialogue in here, just like you can for many other functions scattered through the .dic files. If you only want the one, just delete the others, or if you want to add more, just copy and paste and write new dialogue as it comes to you. This applies to when the recycle bin is empty as well.
	}
}


//This is for the SAORI, don't touch it.
OnGarbagecanEmpty
{
	_gomi = FUNCTIONEX("gomi.dll", "-f")
}

//This is when the user chooses System Information. Don't touch any of this either. For those curious though, note that this is done using another SAORI, this time saori_cpuid.dll.
Select.ChoiceSystemInformation
{
	os_name = FUNCTIONEX("saori_cpuid.dll","os.name")
	os_version = FUNCTIONEX("saori_cpuid.dll","os.version")
	cpu_name = FUNCTIONEX("saori_cpuid.dll","cpu.name")
	cpu_clockex = FUNCTIONEX("saori_cpuid.dll","cpu.clockex")
	"/
	\0\s[0]\b[2]/
	\_q\f[bold,1]OS:\f[bold,0] %(os_name) %(os_version)\n/
	\f[bold,1]Memory Load:\f[bold,0] %(memoryload)%\n/
	\f[bold,1]Physical Memory:\f[bold,0] %(memoryavailphys) KB\n/
	\f[bold,1]CPU:\f[bold,0] %(cpu_name) %(cpu_clockex)MHz\n/
	\f[bold,1]Ukagaka:\f[bold,0] %property[baseware.version]/
	\e"
}
	
//This is when the user chooses to check their email. The rest of this function plays out in etc.dic. You can leave this alone.
Select.MailCheck
	{
		"\![biff]\e"
	}


	
//---- OnChoiceTimeout ---------------------------------------------------------
//If the user is given a menu with choices, but doesn't pick one of the choices, it will eventually time out. At that point, they will say this dialogue. Replace with whatever you like, or leave it blank.

OnChoiceTimeout
 {
	"\0\s[9]Time's up. I'm closing the menu.\e"
	"\0\s[9]That's enough. Make up your mind and then choose something, hm?\e"
	"\0\s[9]We're done here. I'm closing the menu."
 }



//    ___ _                        
//   /___\ |_ __ _  ___ ___  _ __  
//  //  // __/ _` |/ __/ _ \| '_ \ 
// / \_//| || (_| | (_| (_) | | | |
// \___/  \__\__,_|\___\___/|_| |_|
//---- Otacon's Menu ---------------------------------------------------------
//This section has to do with the menu for the second character. If you don't want your second character to have a menu, you can just delete this whole chunk.

OpenKeroMenu
 {
	if codecTimer > 1 || missionTimer > 1 
	{
		"\1\s[38]\b[2]"
	}
	else
	{
		"\1\s[10]\b[2]"
		"\1\s[15]\b[2]"
		"\1\s[16]\b[2]"
	}
	//You've seen so far that you can set up multiple options for menu introductions. You can also set up multiple poses to match up to multiple dialogue. So for example, when you open Otacon's menu, he may randomly do any of these three poses, and randomly choose from any of the four dialogue options below, before continuing with his menu. Make a close note of the -- dashes breaking these up.
 	--
	"Hey, %(username)! What do you need?"
 	"What's up, %(username)?"
 	"Hey, %(username)! Let's see... I have a couple options, here."
	if codecTimer > 1 || missionTimer > 1
	{
		"I'm a little busy, sorry. I'll see what I can do. What do you need?"
	}
 	--
	"\n\n[half]/
	\![*]\q[Update,keroupdatecheck]\n/
 	\![*]\q[Bug report,bugreport]\n\n/
	\![*]\q[Interact,interactKERO1]\n"
	--
	if otaconFeelings >= 40 && devMode == 1 //Menu to initiate codec search.
	{
		"\![*]\q[Codec Search,codecSearch]\n"
	}
	--
	if mk2 == "owned" && devMode == 1 //Menu for bringing items earned through gameplay out.
	{
		"\![*]\q[Bring Items,bringClutter]\n"
	}
	--
	if otacoins > 0 && devMode == 1 //Shop for Otacoins
	{
		"\![*]\q[Shop,buy]\n"
	}
	--
	"\![*]\q[FAQ,faq]\n/
	\![*]\q[Ghost Tips,tips]\n\n"
	--
	if devMode == 1
	{
	//Dev Testing options begin. Only viewable if devMode == 1
	"\![*]\q[Test variableDEVTEST ONLY,testvariable]\n/
	\![*]\q[View variablesDEVTEST ONLY,viewvariable]\n/
	\![*]\q[Change variablesDEVTEST ONLY,changevariable]\n/
	\![*]\q[Run SurfacesDEVTEST ONLY,testKEROsurfaces]\n\n"
	}
	--
	"\![*]\q[Nothing,keroCANCEL]/
	\e"
}

//There are no real set things for the second character to do, really. You can mostly add whatever functions you want to them, their menu is totally free reign. In mine for example, Hunter has some games, a pseudo-FAQ for users, a bug report, an update option, and most importantly, a test variable. A lot of that is fairly complicated though, so I won't get into it here. Advanced Users can pick up Hunter and Smoker and study their menu.dic to see how I did it though, and those who read the coding page of the walkthrough and want to make their own functions can feel free to add them here. 
//For the purposes of this simple template, I'll include two options that may come in handy - an update option and a bug report option, as well as the test variable.

//---- Mundane Options ---------------------------------------------------------

//If you click cancel. Replace dialogue as you want.
Select.keroCANCEL
{
	if codecTimer > 1 || missionTimer > 1
	{
		"\1\s[38]"
		--
		"Nothing caught your eye?\e"
		"All done?\e"
		"Nothing left to do.\e"
		"Alright, I'll close the menu then.\e"
	}
	else
	{
		"\1\s[18]Nothing caught your eye?\e"
		"\1\s[18]All done?\e"
		"\1\s[32]Nothing left to do.\e"
		"\1\s[18]Alright, I'll close the menu then.\e"
	}
}


//This is when they select bug report. This is mostly meant to briefly give the user information about you, the creator, and how to report a problem to you. This can be handy if the person forgot where they got their ghost or how to contact you. Add whatever info you like here.
Select.bugreport
{
"\1\s[10]Found a bug, huh? Jeez, no wonder I feel a little weird.\w5 You can report bugs to Okuajub.Reports@protonmail.com\n\w5We were downloaded from \_a[https://okuajub.github.io/pcPets/]Here.\_a Our craftsman is Okuajub.\e"
}

//When clicked, this will automatically tell the ghost to check for updates. More detail on updates in etc.dic. You can leave this alone.
Select.keroupdatecheck
{
	"\1\s[10]\![updatebymyself]\e"
}

//   ___          _             __                     _     
//  / __\___   __| | ___  ___  / _\ ___  __ _ _ __ ___| |__  
// / /  / _ \ / _` |/ _ \/ __| \ \ / _ \/ _` | '__/ __| '_ \ 
/// /__| (_) | (_| |  __/ (__  _\ \  __/ (_| | | | (__| | | |
//\____/\___/ \__,_|\___|\___| \__/\___|\__,_|_|  \___|_| |_|
//---- Codec Search ---------------------------------------------------------
//This is a unique feature for Otacon. Codec Search lets the user input an amount of time for Otacon to search for new codec frequencies.
Select.codecSearch
{
	if codecTimer == "" && codecFatigue == 0 //Checks to see if the codec timer is ready to be reset. if codecTimer is blank, it means its ready to recieve information. if codecFatigue is still going, it means Otacon is tired and it needs to reach 0 first.
	{
		"\1\s[18]Oh, you want me to search through the codec frequencies? I can do that. How long do you want me to search for? In minutes, please.\n\n[half]\f[italic,true]How long will Otacon search?\![open,inputbox,OnSetCodecTimer,-1]"
	}
	elseif codecTimer == "finished"
	{
		codecFatigue = (inputTime * 0.5) + RAND(600) + 600 //Sets the fatigue timer, which is the same as half of the inputted minutes (in seconds) plus 0-600 seconds, plus 600 seconds (10 minutes). Max fatigue is 1 hour and 20 minutes, if I remember right.
		codecTimer = "" //Makes sure codecTimer is ready to recieve new information.
		"\1\s\[15]Alright! Like I said, I finished up searching. "
		if inputTime <= 2700 //Less than or 45 Minutes
		{
			if RAND(100) > 69 && mk2 != "owned" //nice
			{
				"I found some good information on an old connection of mine- I managed to recover some blueprints for a project! It's the MG MK. II. You can ask me to bring it out in my double click menu. Oh, and If you're wondering about which one is the MK. I... Er, don't think about it too hard.\n\n[half]"
				mk2 = "owned" //Gives the player access to the MK. II
			}
			elseif mk3 != "owned" && mk2 == "owned"
			{
				"I got some ideas for some improvements to the MK. II while listening to an engineering project's managment meeting.\s[15] Don't worry, no plagarism! Stealing other's concepts is just rude... Anyway. You should be able to bring out the new version of the MK. II in the menu, under the name MK. III!"
				mk3 = "owned"
			}
			else
			{
				"\1\s[37]I didn't find anything unique this time. Sorry.\w4\w4\s[15] But I did find some Otacoins!"
				--
				if otacoins < 1
				{
					"\n\n[half]\f[italic,true]You say you have no idea what that is.\f[italic,default]\n\n[half]\s[18]You don't? Oh, duh! I forgot to tell you. Well, Otacoin isn't a real currency. Not in the monetary economy, anyway. It's not a cryptocurrency, either! (I just thought the name was funny..) Anyway. They're.. bargaining chips of sorts. Information I scrounged up and can be exchanged for real money or goods.\s[15] Don't worry, I'll handle it and be the middleman in those sorts of things!\n\n[half]\f[italic,true]You say it sounds a bit like blackmail.\f[italic,default]\n\n[half]\s[2501]N-No! That's not it at all...\w8\w8\s[16] Sometimes I sell the information to \f[italic,true]o\w2th\w4er\f[italic,default] people... So it's not blackmail, okay?"
				}
				--
				_otacoinPlus += RAND(80) + 20
				"\n\n[half]\f[italic,true]You Recieved %(_otacoinPlus) Otacoins! "
				otacoins += _otacoinPlus
				--
				"\f[italic,true]You now have %(otacoins) Otacoins."
			}
		}
		elseif inputTime > 2701 && inputTime < 4200 //46 minutes to an hour and 10 minutes
		{
			"This is dialogue for when you've searched between 46 minutes and an hour and ten minutes. Wow!"
			--
			if otacoins < 1
			{
				"\n\n[half]\f[italic,true]You say you have no idea what that is.\f[italic,default]\n\n[half]\s[18]You don't? Oh, duh! I forgot to tell you. Well, Otacoin isn't a real currency. Not in the monetary economy, anyway. It's not a cryptocurrency, either! (I just thought the name was funny..) Anyway. They're.. bargaining chips of sorts. Information I scrounged up and can be exchanged for real money or goods.\s[15] Don't worry, I'll handle it and be the middleman in those sorts of things!\n\n[half]\f[italic,true]You say it sounds a bit like blackmail.\f[italic,default]\n\n[half]\s[2501]N-No! That's not it at all...\w8\w8\s[16] Sometimes I sell the information to \f[italic,true]o\w2th\w4er\f[italic,default] people... So it's not blackmail, okay?"
			}
			--
			_otacoinPlus += RAND(60) + 40
			"\n\n[half]\f[italic,true]You Recieved %(_otacoinPlus) Otacoins! "
			otacoins += _otacoinPlus
			--
			"You now have %(otacoins) Otacoins."
			
		}
		elseif inputTime >= 4200 && inputTime < 5400 // hour and ten to hour and a half
		{
			"This is dialogue for when you searched between an hour and 11 minutes to an hour and a half. Pretty long now, huh!"
			--
			if otacoins < 1
			{
				"\n\n[half]\f[italic,true]You say you have no idea what that is.\f[italic,default]\n\n[half]\s[18]You don't? Oh, duh! I forgot to tell you. Well, Otacoin isn't a real currency. Not in the monetary economy, anyway. It's not a cryptocurrency, either! (I just thought the name was funny..) Anyway. They're.. bargaining chips of sorts. Information I scrounged up and can be exchanged for real money or goods.\s[15] Don't worry, I'll handle it and be the middleman in those sorts of things!\n\n[half]\f[italic,true]You say it sounds a bit like blackmail.\f[italic,default]\n\n[half]\s[2501]N-No! That's not it at all...\w8\w8\s[16] Sometimes I sell the information to \f[italic,true]o\w2th\w4er\f[italic,default] people... So it's not blackmail, okay?"
			}
			--
			_otacoinPlus += RAND(60) + 80
			"\n\n[half]\f[italic,true]You Recieved %(_otacoinPlus) Otacoins! "
			otacoins += _otacoinPlus
			--
			"You now have %(otacoins) Otacoins."
		}
		else
		{
			"\1\s[10]Woah, you searched more than an hour and a half!"
			--
			if otacoins < 1
			{
				"\n\n[half]\f[italic,true]You say you have no idea what that is.\f[italic,default]\n\n[half]\s[18]You don't? Oh, duh! I forgot to tell you. Well, Otacoin isn't a real currency. Not in the monetary economy, anyway. It's not a cryptocurrency, either! (I just thought the name was funny..) Anyway. They're.. bargaining chips of sorts. Information I scrounged up and can be exchanged for real money or goods.\s[15] Don't worry, I'll handle it and be the middleman in those sorts of things!\n\n[half]\f[italic,true]You say it sounds a bit like blackmail.\f[italic,default]\n\n[half]\s[2501]N-No! That's not it at all...\w8\w8\s[16] Sometimes I sell the information to \f[italic,true]o\w2th\w4er\f[italic,default] people... So it's not blackmail, okay?"
			}
			--
			_otacoinPlus += RAND(60) + 160
			"\n\n[half]\f[italic,true]You Recieved %(_otacoinPlus) Otacoins! "
			otacoins += _otacoinPlus
			--
			"You now have %(otacoins) Otacoins."
		}
		--
		"\f[italic,default]I'll probably be ready to search again in about %(inputTime / 60) minutes."
	}
	elseif codecFatigue >= 1
	{
		"\1\s[33]Sorry, %(username). I'm still pretty exhausted from that last task. Do you mind waiting a bit? I think I'll be ready to go again in "
		--
		if codecFatigue < 600
		{
			"a little bit."
		}
		elseif codecFatigue >= 600 && codecFatigue < 1500
		{
			"about twenty minutes?"
		}
		elseif codecFatigue >= 1500 && codecFatigue <= 2700
		{
			"forty-five minutes, maybe."
		}
		elseif codecFatigue > 2700 && codecFatigue <= 3200
		{
			"maybe an hour?"
		}
		else
		{
			"an hour or two."
		}
	}
	elseif missionFatigue >= 1
	{
		"\1\s[33]Sorry, %(username). I'm still pretty exhausted from that last mission. Do you mind waiting a bit? I think I'll be ready to go again in"
		-- //I could probably just diverge this in the original one and replace "mission" or "task" with an if check but... it's not worth the effort, haha.
		if codecFatigue < 600
		{
			"a little bit."
		}
		elseif codecFatigue >= 600 && codecFatigue < 1500
		{
			"about twenty minutes?"
		}
		elseif codecFatigue >= 1500 && codecFatigue <= 2700
		{
			"forty-five minutes, maybe."
		}
		elseif codecFatigue > 2700 && codecFatigue <= 3200
		{
			"maybe an hour?"
		}
		else
		{
			"an hour or two."
		}
	}
	else
	{
		if codecTimer > 1
		{
			"\1\s[38]Sorry, I'm already working on it! I'll be done in "
			--
			if codecTimer < 1800
			{
				"less than thirty minutes!"
			}
			elseif codecTimer > 1800 && codecTimer <= 3200
			{
				"less than an hour. Okay?"
			}
			else
			{
				"maybe an hour or two?"
			}
		}
	}
	--
	"\e"
}
OnSetCodecTimer //maybe I should have used something other than "thing".
{
	"\1\s[18]\f[italic,true]You tell Otacon %(reference0) minutes sounds right.\f[italic,default]\n\n[half]"
	--
	inputTime = reference0 * 60 //Turns the minutes the user inputs into seconds, so that when we set codecTimer later its really simple.
	
	if inputTime > 7201
	{
		"Sorry, %(username). I can't really search for longer than two hours or so... Manually flipping through all of the frequencies is really tiring. At least thirty minutes should work."
	}
	elseif inputTime < 1799
	{
		"\1\s[37]I won't really find anything useful in that short of time, sorry. I need probably at least 30 minutes to have a chance to find something interesting? And I can go for about two hours at the longest."
	}
	elseif inputTime >= 1800 && inputTime <= 7200
	{
		if inputTime == 4140
		{
			"nice.\n\n[half]"
		}
		--
		"\1\s[15]Alright! That sounds just fine. I'll get to working on that.\w8\w8\1\s[38]"
		codecTimer = inputTime
	}
	elseif inputTime == 0 || reference0 == ""
	{
		inputTime = (RAND(90) + 30) * 60
		"\1\s[18]Er... I'll just choose myself then. I'll look for %(inputTime / 60) minutes."
		codecTimer = inputTime
	}
	else
	{
		"\1\s[18]Sorry, it got kind of garbled there. Could you repeat what you said? Make sure you input it in with just numbers, like '120' for two hours."
	}
	--
	"\e"
}

//If you want to do a system like this.. please don't do it like me. Learn how to use arrays before you write this kind of spaghetti.
Select.bringClutter : all
{
	"\1\s[10]Okay! I can do that. Which items do you want to bring out?\b2\n\n"
	if devMode == 0
	{
		//display owned
		if mk2 == "owned"
		{
			"\![*]\q[Mk. II,bringMk2]\n"
		}
		if mk3 == "owned"
		{
			"\![*]\q[Mk. II,bringMk3]\n"
		}
	}
	else //if in Devmode, all are displayed regardless of ownership
	{
		"\![*]\q[Mk. II,bringMk2]\n"
		--
		"\![*]\q[Mk. II,bringMk3]\n"
	}
	"\![*]\q[Nothing,keroCANCEL]"
}

Select.bringMk2
{
	"\1\s[10]Can do. Here you go, little buddy!\p[3]\s[9910]\1\s[15] Have fun!"
}

Select.bringMk3
{
	"\1\s[10]Sure thing. New and improved little buddy, here you come!\p[4]\s[9911]"
}

//Shop for spending Otacoins
Select.buy
{
	"Alright!"
}

//---- Otacon's FAQ ---------------------------------------------------------
Select.faq //OKUAJUB: FAQ, unlocks more answers as you listen.
{ 
	if codecTimer > 1 || missionTimer > 1
	{
		"\1\s[38]"
		--
		"Sorry, I can't answer your question right now... Um, can you ask me later? Thanks."
		"Huh? Oh, darn, I lost my train of thought. What was I..? Um, sorry. I'll be done soon, I think."
		"Uh... I'll get back to you.. in a second..."
	}
	"\1\s[18]\b[2]Got a question for me? Shoot.\n/
	\![*]\_a[whoSnake]Who's Snake?\_a\n/
	\![*]\_a[whoHal]Who's Otacon?\_a\n"
	--
	//'Who" section
	if clickMeiLing >= 1
	{
		"\![*]\_a[whoMeiLing]Who's Mei Ling?\_a\n"
	}
	--
	if clickRaiden >= 1
	{
		"\![*]\_a[whoRaiden]Who's Raiden?\_a\n"
	}
	--
	if otaconFeelings >= 50
		"\![*]\_a[whoEmma]Who's Emma?\_a\n"
	--
	if otaconFeelings >= 80
		"\![*]\_a[whoJulie]Who's Julie?\_a\n"
	--
	//'What' section
	if clickShadowMoses >= 1 
	{
		"\![*]\_a[whoShadowMoses]What's Shadow Moses?\_a\n"
	}
	--
	if clickRex >= 1
	{
		"\![*]\_a[whoRex]What's REX?\_a\n"
	}
	--
	"\![*]\q[Nevermind,keroCANCEL]\x\e"
}

//---- Otacon's Usage Tips ---------------------------------------------------------
//OKUAJUB: one of Snake and Otacon's main features is to be beginner friendly. So, I thought I might add that option to view specific tips here.
//OKUAJUB: as another note: I really suggest just making these functions, if you're looking through this to study. I didn't quite know how to do that when I wrote this, but as you can see I quickly learned. I might clean this up in the future. bleh. The FAQ is anchors for a reason, though. I can just point to the same anchors I use in their random talk.
Select.tips
{
	
	if codecTimer > 1 || missionTimer > 1
	{
		"\1\s[38]"
		--
		"Er, Sorry. I'm still working on this. Can you ask me when I'm done?"
		"Sorry, %(username). I have to focus for a... second..."
		"Er, what was that? Sorry, I'm a... little busy..."
	}
	else
	{
		"\1\s[15]\b2Oh, Yeah! Do you have anything specific you want to ask me about?\n"
		--
		"\f[italic,true]How do I...\f[italic,default]\n/
		\![*]\_a[talkingTip]Speed up talking?\_a\n/
		\![*]\_a[wallpaperTip]Set my wallpaper?\_a\n/
		\![*]\_a[emailTip]Check my email?\_a\n/
		\![*]\_a[trashTip]Empty the recycle bin?\_a\n/
		\![*]\_a[rssTip]Use RSS Feeds?\_a\n/
		\![*]\_a[specsTip]Find system information?\_a\n/
		\![*]\_a[iconifyTip]Use Iconify and minimize the Ghost?\_a\n/
		\![*]\_a[hitpetTip]Hit or Pet my Ghost?\_a\n/
		\![*]\q[Nevermind,keroCANCEL]\x\e"
	}
}

//---- Otacon's Interaction Menu(s) ---------------------------------------------------------
Select.interactKERO1
{
	if codecTimer > 1 || missionTimer > 1
	{
		"\1\s[38]"
		--
		"Huh? Oh, sorry. I can't do anything with you right now... I'm still working on this."
		"...\n...\nOh, did you say something? Sorry, sorry. It's hard for me to hear you..\w4 when I'm... working.. \f[size,8]what was I doing..?\f[size,default]"
		"What was that? Er, sorry. This takes a lot of focus."
		"Um, sorry, I can't talk right now."
	}
	else
	{
		"\1\s[18]\b2Huh?\n/
		\_q\f[italic,true]Give...\f[italic,default]\n/
		\![*]\q[Cigarettes,giveCigsKERO]\n/
		\![*]\q[Ration,giveRationKERO]\n/
		\![*]\q[Ketchup,giveKetchKERO]\n/
		\![*]\q[Something Else?,giveSomethingKERO]\n"
		--
		if otaconFeelings <= -50
		{
			"\![*]\q[An Apology,sayApologyKERO]\n"
		} 
		--
		"\![*]\q[Nevermind,keroCANCEL] \![*]\q[Next Page,interactKERO2]\n\n/
		\f[italic,true]Inventory\n/
		Ketchup %(snakeKetchups)/20\f[italic,default]/
		\e"
	}
}
Select.interactKERO2
{

	"\1\s[18]\b2Huh?\n/
	\_q\f[italic,true]Play...\f[italic,default]\n/
	\![*]\q[Rock Paper Scissors,playRPS]\n/
	\![*]\q[Suit Speculation,playSuits]\n/
	\![*]\q[Higher or Lower?,playHighLow]\n/
	//\![*]\q[Sticks,playSTICKS]\n/ Still working on this one.
	\f[italic,true]Say...\f[italic,default]\n/
	\![*]\q[An insult,sayInsultKERO]\n/
	\![*]\q[A compliment,sayComplimentKERO]\n/
	\![*]\q[A joke,sayJokeKERO]\n/
	\![*]\q[Nevermind,keroCANCEL] \![*]\q[Last Page,interactKERO1]\n\n/
	\f[italic,true]Inventory\n/
	Ketchup %(snakeKetchups)/20\f[italic,default]/
	\e"
}

//---- Otacon's Interactions ---------------------------------------------------------

//---- Giving to Otacon ---------------------------------------------------------

//Giving Otacon Cigarettes.
Select.giveCigsKERO
{
	otaconFeelings--
	"\1\s[18]\f[italic,true]You hand Otacon a pack of cigarettes. \f[italic,default]"
	--
	if otaconFeelings <= -30
	{
		"\1\s[31]\f[italic,true]Otacon looks at you, unimpressed.\f[italic,default]\n\n[half]...You're just trying to get a rise out of me.\n\n\f[italic,true]Otacon throws the cigarettes into the Recycle Bin.\f[italic,default]"
		--
		if RAND(100) < 2 // 3% chance of this dialogue happening, supposedly.
		"\n\n\1\s[15]Kenobi!\f\[size,8] Or something. I don't really know sports.\f[size,default]"
		--
		"\e"
	}
	--
	if otaconFeelings <= -10
	{
		{
			"\1\s[18]\f[italic,true]Otacon looks at them, unimpressed.\f[italic,default]\n\n[half]"
			--
			"...Why are you giving me these?\e"
			"I don't smoke.\e"
		}
		"\1\s[17]\f[italic,true]Otacon doesn't even look at them.\f[italic,default]\n\n[half]Stop giving me cigarettes! I won't smoke!\e"
	}
	--
	if otaconFeelings >= -9
	{
		"\1\s[19]\f[italic,true]Otacon looks at them, unimpressed.\f[italic,default]\n\n[half]"
		--
		"You know I don't smoke, right? It's bad for you.\e"
		"Smoking causes lung cancer, %(username). It says it right here on the package.\e"
		"My mouth isn't an ashtray.\e"//I actually got this line from an old anti-smoking poster for schools. I like to imagine Hal saw this in highschool and accidentally quotes it from his subconcious, sometimes.
	}
	--
	if otaconFeelings > 20
	{
		"\1\s[18]\f[italic,true]Otacon looks at them, unimpressed."
		--
		"\f[italic,default]\n\n[half]I'm not sure why you're giving me this.\e"
		"\f[italic,default]\n\n[half]I don't think I need to tell you I don't want this.\e"
		" He just sighs.\f[italic,default]\e"
		"\f[italic,default]\n\n[half]Look, if you need to dump your cigarettes, dispose of them properly. Don't give them to me.\e"
	}
	--
	if snakeFeelings >= 30
	{
		"\1\s[19]\f[italic,true]Otacon looks at them, unimpressed.\f[italic,default]\n\n[half]Lucky Strike? Did you mean to give these to Snake?\e"
		"\1\s[19]\f[italic,true]Otacon looks at them, unimpressed.\f[italic,default]\n\n[half]Moslems? Did you mean to give these to Snake?\e"
	}
}

//Giving Otacon Rations. Requires ketchup for positive feelings, as Otacon is a bit more picky about it than Snake.
Select.giveRationKERO
{
	"\1\s[18]\f[italic,true]You hand Otacon an MCI ration.\f[italic,default]"
	--
	if snakeKetchups < 1
	{
		otaconFeelings--
		"\1\s[14]\f[italic,true] Otacon wrinkles his nose, despite the fact it's unopened.\f[italic,default]"
		--
		{
			"\1\s[14]\n\n[half]This stuff tastes like cat food..."
			--
			if isNeko = 1
			{
				" and just because I'm a nekomimi doesn't mean I like it..."
			}
		}
		"\1\s[14]\n\n[half]I'll... save this for later."
		"\1\s[14]\n\n[half]Er, sorry. My stomach is a bit sensitive..."
		"\1\s[14]\n\n[half]Uhh... Do you want this, Snake..?\0\s[9]"
	}
	if snakeKetchups >= 1
	{
		otaconFeelings += 3
		snakeKetchups--
		"\1\s[18]\n\n[half]Huh, I bet the ketchup would make this taste better. Thanks for planning ahead, %(username)?"
		"\1\s[18]\n\n[half]That ketchup you gave us should come in handy, huh, %(username)?"
		"\1\s[30]\n\n[half]Er, I'm just going to borrow some of that ketchup %(username) gave us, Snake. Don't mind me diving into your pockets...\0\s[5]Just don't get too friendly, hm?"
	}
	--
	"\e"
}

//Giving Otacon Ketchup. Required for positive gain via giving menu. Also checks to make sure the ketchup has not hit the cap.
Select.giveKetchKERO
{

	"\1\s[18]\f[italic,true]You hand Otacon a bottle of ketchup.\f[italic,default]\n\n"
	--
	if snakeKetchups == 0
	{
		"\1\s[18]Huh. Reminds me of Shadow Moses."
		"\1\s[18]Thank you?"
		"\1\s[10]Um, Thanks for the ketchup."
		otaconFeelings++
	}	
	elseif snakeKetchups == 1
	{
		"\1\s[18]Uh, thank you? Er, Snake, can you hold this? I don't actually have that many pockets..."
	}
	elseif snakeKetchups == 2
	{
		"\1\s[18]Alright..."
	}
	elseif snakeKetchups >= 3 && snakeKetchups <= 6
	{
		"\1\s[19]This is weird."
		"\1\s[13]This better not be some fetish thing."
		"\1\s[13]What's this for, %(username)?"
		"\1\s[19]Come on, I don't need any more of this stuff."
		"\1\s[19]What's up with you and ketchup, %(username)?"
	}
	elseif snakeKetchups == 7
	{
		"\1\s[18]Lucky number seven, huh? Are you superstitious or something?"
	}
	elseif snakeKetchups >= 8 && snakeKetchups < 20
	{
		"\1\s[13]Are you trying to freak me out?"
		"\1\s[13]Come on, %(username)! This is getting really weird!"
		"\1\s[19]Do you have a plan for all this ketchup, even, %(username)?"
		"\1\s[13]Where are you getting all this stuff..."
		"\1\s[19]You've got to be getting all this ketchup from somewhere. Let me guess, %(username), your dad works at Heinz? Must be something like that..."
		--
		if isNeko = 1
		{
			"\1\s[18]Er, is it safe for cats to have this much tomato?"
			"\1\s[19]You're not trying to imply something about 'catsup', are you?"
		}
	}
	elseif snakeKetchups == 20
	{
		"\1\s[13]Uh... I don't think we can physically carry any more of the stuff. Sorry. \f[size,8]This is so weird...\f[size,default]"
	}
	--
	if snakeKetchups < 20
	{
		snakeKetchups++
	}
	--
	"\e"
}

Select.giveSomethingKERO //Begins giving Otacon "thing"s. "thing"s are whatever the user generates. 
{
	"\1\s[18]\f[italic,true]You try to give Otacon something. What is it?\![open,inputbox,OnGiveThingKERO,-1]"
}
OnGiveThingKERO //maybe I should have used something other than "thing".
{
	"\1\s[18]\f[italic,true]You try to give Otacon %(reference0).\f[italic,default]\n\n[half]"
	--
	reference0 = TOLOWER(reference0) //changes it to lowercase
	reference0 = REPLACE(reference0," ","") //removes spaces
	reference0 = REPLACE(reference0,".","") //removes periods
	reference0 = REPLACE(reference0,"-","") //removes dashes (all these stolen from namesTeach)
	thing = reference0 //This sets whatever the user inputted (reference0) to variable "thing" so I can use it for checks and stuff. just makes things (no pun intended) easier.
	
	if thing == "subs"
	{
		"\1\s[15]Oh! I like this thing."
		--
		"\n\n[half]\f[italic,true]Otacon seems very pleased."
	}
	elseif thing == "dubs"
	{
		"\1\s[18]Oh. I don't like this thing."
	}
	elseif thing == "turgle" || thing == "turgled" || thing == "turgling"
	{
		"\1\s[18]\0\s[8]Who turgled?"
	}
	elseif thing == "otacon"
	{
		"\1\s[18]\f[italic,true] But nothing comes out.\f[italic,default]\n\n[half]"
		--
		"\1\s[18]Hey, I saw that input. Don't try making clones of me too, now!"
		"\1\s[32]Huh. Imagine if that did work? I don't know if I'd like it."
	}
	elseif thing == "snake" || thing == "twinsnakes"
	{
		"\1\s[18]\f[italic,true]The computer thinks for a moment before dropping Otacon a gummy worm.\f[italic,default]\n\n[half]...What? That is... weird."
		"\1\s[18]\f[italic,true]The computer thinks for a moment before dropping Otacon a sour gummy worm.\f[italic,default]\n\n[half]...What? That is... weird." //if you don't get it, this is a reference to a candy made by Haribo known as "Twin Snakes", I thought it was a funny in-joke.
	}
	elseif thing == "solidsnake"
	{
		"\1\s[18]\f[italic,true]But nothing comes out.\f[italic,default]\n\n[half]"
		--
		if askLove == 6 || (snakeFeelings > 50 && otaconFeelings > 50)
		{
			"\1\s[18]Huh.\w8\w8\w5\0\s[5]Otacon already \f[italic,true]has\f[italic,default] me. In fact, he's had me many times.\1\s[11]\n\n[half]Snake!!! Oh my god... You-!\w8\1\s[13]\w8\w8\w8\1\s[32]\w8\w8 That was so bad.... You are one of a kind, Snake." //lol
		}
		else
		{
			"You're not trying to clone him now, too, are you?"
		}
	}
	elseif thing == "triangle" || thing == "girl"
	{
		"\1\s[18]\f[italic,true] The computer spits out "
		--
		"a tutorial on how to make an ukagaka.\f[italic,default]\n\n\[half]Now why would I need a thing like that?"
		"a webcomic strip of a girl and a triangle.\f[italic,default]\n\n[half]Do I know these guys? They feel familiar."
	}
	elseif thing == "help" || thing == "newword" || thing == "info" || thing == "words" || thing == "word" || thing == "something"
	{
		"\1\s[18]\f[italic,true] The computer spits out a piece of paper with "
		--
		"the word Subs."
		"the word Dubs."
		"the word Turgle."
		"the word Triangle."
		"the word Girl."
		"the word Snake."
		--
		"\1\s[18]\f[italic,default]\n\n[half]Huh. Maybe this is a keyword it knows? It's not \f[italic,true]that\f[italic,default] helpful..."
	}
	elseif thing == "testingcheatsenabledtrue" || thing == "password" //haha sims reference
	{
		"\1\s[15]Oh! Hey, Okuajub! I'll go ahead and set Development mode to true (1). If you're not... Okuajub... then don't mess anything up, okay? You can turn dev mode off by telling me 'testingcheatsenabled false' or just '12345' if that's too long. If you continue in this mode you might encounter some weird things Okuajub uses during development, or unfinished stuff. Remember, it's '12345' to turn it off again.\e"
		devMode = 1
	}
	elseif thing == "testingcheatsenabledfalse" || thing == "12345"
	{
		"\1\s[18]Alright, I've turned devmode back off again. Good luck, %(username)!\e"
		devMode = 0
	}
	else
	{
		"\1\s[18]\f[italic,true] But nothing comes out.\f[italic,default]\n\n[half]"
		--
		"\1\s[37]Nothing?" 
		"\1\s[37]Change your mind?"
		"\1\s[37]Sorry, I didn't get anything. Did something go wrong on your end?"
	}
	--
	"\e"
}
//Giving Otacon an Apology. Resets feelings to 0. Planning on expanding this to be more fleshed out, like Snake's, but this is fine.
Select.sayApologyKERO
{
	otaconFeelings = 0
	"\1\s[14]If... if you really mean it, then I accept it.\e"
}

//---- Playing Games with Otacon ---------------------------------------------------------

//Quits games.
Select.quitGame
{
	"\1\s[33]Aw, well, thanks for playing with me! It was fun.\e"
}

//---- Rock, Paper, Scissors ---------------------------------------------------------
//Simple Rock, paper, scissors game with Otacon. Not entirely sure what to do with it yet, but I challenged myself to write it in an hour. (If you're curious, I did, haha.) Otacon's also not a sore loser, so no feelings are affected.
//FUTURE OKUAJUB: oh god the code for this got kind of messy. I promise it looked simpler before. adding the rules and everything got it a bit more complicated. It's really easy to make it simpler, though. Just have an if/elseif chain for each choice, and have it roll RAND(2), lose == 0. draw == 1, win == 2. you know. I just get silly, haha. I'll clean this up someday. For now, it works.
Select.playRPS
{
	"\1\s[15]Alright, %(username)!\n /
	\![*]\q[Rock,rockRPS], /
	\![*]\q[Paper,paperRPS], or /
	\![*]\q[Scissors,scissorsRPS]?\n/
	Or do you want me to\n/
	\![*]\q[explain the rules,rulesRPS]?/
	\e"
}
//Rules for Rock, Paper, Scissors. Read to learn how the base game works.
Select.rulesRPS 
{
	"\1\s[15]Okay, Rock, Paper, Scissors is pretty easy to learn. We'll both pick either rock, paper, or scissors and then we declare them at the same time!\1\s[18] If we both threw the same kind, such as rock and rock, it's a tie.\1\s[15] If it's not a tie, then one of each kind is better against another. Rock beats scissors, scissors beats paper, and paper beats rock! That's the name of the game, and how you win!/
	\nAlright, are you ready to \![*]\q[play,playRPS]?\n/
	\![*]\q[I'm done,quitGame]\e"
}
//Player picks Rock
Select.rockRPS //Greater win ratio for both of you.
{	
	_otaconScissors = "\f[italic,true]Otacon throws out his hand with a V shape.\n\f[italic,default]" //Used when Otacon throws Scissors
	_otaconPaper = "\f[italic,true]Otacon throws out his hand flat.\n\f[italic,default]" //Used when Otacon throws Paper
	_otaconRock = "\f[italic,true]Otacon throws out his hand in a fist.\n\f[italic,default]" //Used when Otacon throws Rock
	--
	"\1\s[18]\f[italic,true]You throw your hand out in a fist, and \f[italic,default]" // Sets up what action the player is doing, so it is clearer.
	--
	if RAND(100) < 40 //If the random number is under 40 (not including it), Otacon wins. since RAND(100) pulls 0-99, that gives him a 40% chance of winning when Rock is chosen if the random number generator is perfect.
	{
		otaconFeelings++ //Otacon's feelings increase when he wins.
		_otaconPaper
		--
		"\1\s[15]Hehee! Sorry, %(username). Paper beats rock! I win!"
		--
		"\nYou wanna \![*]\q[go again,playRPS]?"
		--
		"\n\![*]\q[I'm done,quitGame]\e"
	}
	elseif RAND(100) > 39 && RAND(100) < 60 //If the random number pulls a number between 39 and 60, it will be a tie.
	{
		_otaconRock
		--
		"\1\s[18]Huh! That's a tie!"
		--
		"\nC'mon, lets \![*]\q[go again,playRPS]?"
		--
		"\n\![*]\q[I'm done,quitGame]\e"
	}
	else //If the random number is not under 40, or 39-60, the player will win.
	{
		_otaconScissors
		--
		"\1\s[18]Aw, your rock beats my scissors.."
		--
		"\nCome on, let's \![*]\q[go again,playRPS]!"
		--
		"\n\![*]\q[I'm done,quitGame]\e"
	}
}
//Player picks Paper
Select.paperRPS //Approximately even ratio. You know, I usually only play Paper in RPS. Doing the same thing over and over works in the short term, and RPS never lasts long.
{
	_otaconScissors = "\f[italic,true]Otacon throws out his hand with a V shape.\n\f[italic,default]"
	_otaconPaper = "\f[italic,true]Otacon throws out his hand flat.\n\f[italic,default]"
	_otaconRock = "\f[italic,true]Otacon throws out his hand in a fist.\n\f[italic,default]"
	--
	"\1\s[18]\f[italic,true]You throw your hand out flat, and \f[italic,default]"
	--
	if RAND(100) <= 33
	{
		otaconFeelings++
		_otaconScissors
		--
		"\1\s[15]Hehee! Sorry, %(username). Scissors beats paper! I win!"
		"\1\s[15]Hehee! Looks like I was the one to turn your paper into snowflakes this time, huh?"
		"\1\s[15]My scissors beat your paper! Let's go!"
		--
		"\nYou wanna \![*]\q[go again,playRPS]?\e"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
	if RAND(100) > 33 && RAND(100) <= 67
	{
		_otaconPaper
		--
		"\1\s[18]Huh! That's a tie!"
		"\1\s[18]We both threw paper..."
		"\1\s[18]A tie... You know, we should turn these sheets of paper into origami... Anyway!"
		--
		"\nC'mon, lets \![*]\q[go again,playRPS]?\e"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
	else
	{
		_otaconRock
		--
		"\1\s[18]Aw, your paper beats my rock.."
		"\1\s[18]I don't get why paper beats rock... What are rocks good for anyway?\0\s[8]Don't be a sore loser, Otacon.\1\s[19]\n\n[half]I'm not!.. Whatever,"
		"\1\s[18]Aw! My rock's... all covered up? Is that how it works?"
		--
		"\nCome on, let's \![*]\q[go again,playRPS]!\e"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
}
//Player picks Scissors
Select.scissorsRPS //Win ratio slightly in the player's favor.
{	
	_otaconScissors = "\f[italic,true]Otacon throws out his hand with a V shape.\n\f[italic,default]"
	_otaconPaper = "\f[italic,true]Otacon throws out his hand flat.\n\f[italic,default]"
	_otaconRock = "\f[italic,true]Otacon throws out his hand in a fist.\n\f[italic,default]"
	--
	"\1\s[18]\f[italic,true]You throw your hand out in a V, and \f[italic,default]"
	--
	if RAND(100) <= 33
	{
		otaconFeelings++
		_otaconRock
		--
		"\1\s[15]Hehee! Sorry, %(username). Rock beats scissors! I win!"
		"\1\s[15]Hehee! Sorry, %(username), my rock banged up your scissors. At least they weren't the fabric scissors, right?"
		"\1\s[15]Looks like I win this time! Rock beats scissors. That was fun!"
		--
		"\nYou wanna \![*]\q[go again,playRPS]?"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
	if RAND(100) >= 34 && RAND(100) <= 64
	{
		_otaconScissors
		--
		"\1\s[18]Huh! That's a tie!"
		"\1\s[18]Aw, we both threw scissors. \f[size,8]We really shouldn't run around with them, let alone throw them around...\f[size,default] Anyway!"
		"\1\s[18]Two scissors, four blades, we're at a tie!"
		--
		"\nC'mon, lets \![*]\q[go again,playRPS]?\e"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
	else
	{
		_otaconPaper
		--
		"\1\s[18]Aw, your scissors beats my paper.."
		"\1\s[18]My paper just got chewed up!"
		"\1\s[18]My paper's in ribbons.."
		"\1\s[18]Darn, you've made snowflakes out of my paper! At least they're pretty."
		--
		"\nCome on, let's \![*]\q[go again,playRPS]!" //I was going to write three for all of the Play Again dialogues, but got bored and figured it didn't matter that much. so Scissors Player Paper Otacon has triple the amount of these than others. ¯\_(ツ)_/¯
		"\nCome on, feeling \![*]\q[lucky,playRPS], %(username)?"
		"\nCome on, I'm sure I can beat you this time. Let's \![*]\q[go again,playRPS]!"
		--
		"\n\![*]\q[I'm done,quitGame]"
	}
	--
	"\e"
}

//---- Suit Speculation ---------------------------------------------------------
//Card Suit guessing game based on the campsite games from Animal Crossing. Simple 1/4 chance of winning.
Select.playSuits
{
	"\1\s[15]Alright, %(username)! Are you ready to choose?\n/
	\![*]\q[Diamonds,DiamondSuits]\n/
	\![*]\q[Hearts,HeartSuits]\n/
	\![*]\q[Clubs,ClubSuits]\n/
	\![*]\q[Spades,SpadeSuits]\n/
	or do you want me to explain the \![*]\q[rules,RulesSuits]?"
}

Select.RulesSuits
{
	"\1\s[15]Okay! Suits Speculation is pretty simple, actually. I'm going to pull a card, and it could be either part of the Diamond, Heart, Club or Spade suits. Your job is to try and guess which one it will be! You'll do this by calling the suit, and then I'll reveal which one it is. If you guess right, you win! If you didn't, then I win. Easy enough, right?/
	 Alright, are you ready to \![*]\q[play,playSuits]?"
}

Select.DiamondSuits
{
	if RAND(99) <= 24 //Picks if the player wins. Approximately 25% chance of winning.
	{
		"\1\s[15]Look at that, you were right! It's a Diamond."
	}
	else
	{
		"\1\s[37]Sorry, %(username)! It's Spades."
		"\1\s[37]Sorry, %(username)! It's Hearts."
		"\1\s[37]Sorry, %(username)! It's Clubs."
	}
	--
	SuitsPlayAgain //Points to the play again function.
}

Select.HeartSuits
{
	if RAND(99) <= 24
	{
		"\1\s[15]Look at that, you were right! It's a Heart."
	}
	else
	{
		"\1\s[37]Sorry, %(username)! It's Spades."
		"\1\s[37]Sorry, %(username)! It's a Diamond."
		"\1\s[37]Sorry, %(username)! It's Clubs."
	}
	--
	SuitsPlayAgain
}

Select.ClubSuits
{
	if RAND(99) <= 24
	{
		"\1\s[15]Look at that, you were right! It's a Club."
	}
	else
	{
		"\1\s[37]Sorry, %(username)! It's Spades."
		"\1\s[37]Sorry, %(username)! It's a Diamond."
		"\1\s[37]Sorry, %(username)! It's Hearts."
	}
	--
	SuitsPlayAgain
}

Select.SpadeSuits
{
	if RAND(99) <= 24
	{
		"\1\s[15]Look at that, you were right! It's a Spade."
	}
	else
	{
		"\1\s[37]Sorry, %(username)! It's a Diamond."
		"\1\s[37]Sorry, %(username)! It's Hearts."
		"\1\s[37]Sorry, %(username)! It's Clubs."
	}
	--
	SuitsPlayAgain
}

SuitsPlayAgain
{
	otaconFeelings++
	"\1\s[15] Do you want to \![*]\q[go again,playSuits] or are you \![*]\q[done,quitGame]?"
}
//---- Higher or Lower ---------------------------------------------------------
//
Select.playHighLow
{
	"\1\s[15]Okay! Higher or lower, I like that one. I'll draw a card and you have to guess if the next one I draw will be higher or lower. An ace is the lowest, at one, and the king is the highest at 13.\n Are you ready to \![*]\q[play,playHighlow2]? Or are you \![*]\q[done,quitGame]?"
}

Select.playHighlow2
{
	highLowNum = RAND(12) + 1
	"\1\s[10]Okay! The first card is...\w8\w8\w8 "
	--
	if highLowNum == 1
	{
		"an ace! That's a 1."
	}
	elseif highLowNum == 11
	{
		"a Jack! That's an 11."
	}
	elseif highLowNum == 12
	{
		"a queen! That's a 12."
	}
	elseif highLowNum == 13
	{
		"a king! That's a 13."
	}
	elseif highLowNum == 8 //Eight specifically because it's the only one besides 11 which needs "an" instead of "a".
	{
		"an 8!"
	}
	else
	{
		"a %(highLowNum)!"
	}
	--
	" So what will it be, \![*]\q[Higher,playHigh] or \![*]\q[Lower,playLow]?"
}

Select.playHigh
{
	_highLowNum2 = RAND(12) + 1
	if _highLowNum2 == highLowNum
	{
		"\1\s[18]Huh! Looks like it's a tie! The next card was %(_highLowNum2). "
	}
	elseif _highLowNum2 < highLowNum
	{
		"\1\s[15]Haha, sweet! I win! I drew %(_highLowNum2). Thanks for playing, %(username). "
		otaconFeelings++
	}
	elseif _highLowNum2 > highLowNum
	{
		"\1\s[14]Aww, it's %(_highLowNum2). You win. Good job guessing! "
	}
	--
	"Do you wanna \![*]\q[go again,playHighlow2] or are you \![*]\q[done,quitGame]?"
}
Select.playLow
{
	_highLowNum2 = RAND(12) + 1
	if _highLowNum2 == highLowNum
	{
		"\1\s[18]Huh! Looks like it's a tie! The next card was %(_highLowNum2). "
	}
	elseif _highLowNum2 > highLowNum
	{
		"\1\s[15]Haha, sweet! I win! I drew %(_highLowNum2). Thanks for playing, %(username). "
		otaconFeelings++
	}
	elseif _highLowNum2 < highLowNum
	{
		"\1\s[14]Aww, it's %(_highLowNum2). You win. Good job guessing! "
	}
	--
	"Do you wanna \![*]\q[go again,playHighlow2] or are you \![*]\q[done,quitGame]?"
}
//---- Sticks ---------------------------------------------------------
//Redacted for now. I still need to bash it into shape. Removed for space.

//---- Otacon's Insulted, Complimented, etc... ---------------------------------------------------------

//---- Insulting Otacon ---------------------------------------------------------
//This may have gotten a bit out of hand, hahh. the player can say some pretty nasty things.
Select.sayInsultKERO
{
	otaconFeelings--
	"\1\s[18]\f[italic,true]"
	--
	if otaconFeelings > -20
	{
		"\1\s[18]You tell Otacon that he's a good-for-nothing nere-do-well."
		"\1\s[18]You tell Otacon he's an idiot."
		"\1\s[18]You tell Otacon he's a stupid weaboo."
		"\1\s[18]You tell Otacon that he's an egghead."
		--
		"\f[italic,false]\n\n"
		--
		"\1\s[17]Hey! What was that for?"
		"\1\s[19]You call that an insult?"
	}
	if otaconFeelings <= -20
	{
		otaconFeelings -= 5
		"\1\s[18]You tell Otacon things would have been better if he switched places with Meryl at Shadow Moses."
		"\1\s[18]You tell Otacon you hope Snake leaves him for someone better."
		"\1\s[18]You tell Otacon you hope he gets hurt on his next mission."
		"\1\s[18]You tell Otacon it's his fault REX became what it is."
		--
		"\f[italic,false]\n\n"
		--
		"\1\s[17]Why would you say something like that?"
		"\1\s[12]Why would you say something like that?"
	}
	if otaconFeelings <= -50 //God these got kind of bad.
	{
		otaconFeelings--
		"\1\s[18]You tell Otacon he should have been born paralyzed like his father."
		"\1\s[18]You tell Otacon he should never have been born."
		"\1\s[18]You tell Otacon it's his fault his father is dead."
		"\1\s[18]You tell Otacon that it was his fault Julie came onto him."
		--
		"\f[italic,false]\n\n"
		--
		"\1\s[12]Oh my god..."
		"\1\s[14]...Maybe you're right."
	}
	if otaconFeelings <= -90
	{
		"\1\s[12]You just yell at Otacon. You're not sure what you're saying."
		"\1\s[12]You tell Otacon you hate him, and you want him to kill himself."
		"\1\s[12]You look at his face and get so angry you start to cry. You don't even need to say anything."
		"\1\s[12]You scream and can't seem to stop screaming, before you're on your hands and knees trying not to cry."
		--
		"\f[italic,false]\n\n"
		--
		"\1\s[12]I-I'm sorry, %(username). I never meant to hurt you like this..."
	}
	--
	"\e"
}

//---- Complimenting Otacon ---------------------------------------------------------
//Feelings boost that scales with Otacon's feelings. For each "level", the player will make a compliment more in line with Otacon's interests, and gain a larger feelings boost. Minimum of +1, so it is not impossible to recover the relationship.
Select.sayComplimentKERO
{
	otaconFeelings++
	
	if otaconFeelings > 50 //Level 5. Player should be familiar with Otacon.
	{
		otaconFeelings += 4 //Adds 4 to the minimum +1 to OtaFeels.
		"\1\s[10]\f[italic,true]You tell Otacon you like the paneling details on his 1/48th RX-78-2 Gundam.\f[italic,default]\n\1\s[30]Ah! Well, thank you. I worked really hard on that one. Due to the larger size the kit needs a bit of a touch-up to make it really shine but... Er. I'm rambling. Thank you."
		"\1\s[10]\f[italic,true]You tell Otacon you're proud of him.\f[italic,default]\n\1\s[30]Oh, um... Thank you?"
		"\1\s[10]\f[italic,true]You tell Otacon you admire his coding ability.\f[italic,default]\n\1\s[30]Oh! Well, it's nothing... I worked hard to get as good as I am, now."
	}
	if otaconFeelings > 20 //Level 4. The player should have learned a little bit about what Otacon likes.
	{
		otaconFeelings += 3
		"\1\s[10]\f[italic,true]You tell Otacon you enjoy his anime selection.\f[italic,default]\n\1\s[30]Thanks! I try and keep it varied. Snake doesn't like to stick to one thing for too long.\s[15] We'd never get through Naruto, haha."
		"\1\s[10]\f[italic,true]You tell Otacon you like his glasses.\f[italic,default]\n\1\s[30]Oh, um, someone else mentioned liking them, too. I guess it's a trend, hah.\w8 Thank you."
	}
	if otaconFeelings > -20 // Level 3. Player starts here on first boot.
	{
		otaconFeelings += 2
		{
		if nowshell == "master"
			{
				"\1\s[10]\f[italic,true]You tell Otacon you like his shirt.\f[italic,default]\n\1\s[30]Oh, um... Thank you?"
			}
		elseif nowshell == "Solid Shell"
			{
				"\1\s[10]\f[italic,true]You tell Otacon you like his shirt.\f[italic,default]\n\1\s[30]Oh, um... thank you!"
			}
		}
		if isNeko = 1
			{
				"\1\s[10]\f[italic,true]You tell Otacon you think he's cute as a catboy.\f[italic,default]\nO-oh! Thanks! I, um, like it too."
			}
		"\1\s[10]\f[italic,true]You tell Otacon you like his hair color.\f[italic,default]\n\1\s[30]Oh, thank you."
	}
	if otaconFeelings <= -20 //Level 2.
	{
		otaconFeelings++
		"\1\s[10]\f[italic,true]You tell Otacon you think his eyes are cute.\f[italic,default]\n\1\s[30]Oh, um... Thank you..?"
	}
	if otaconFeelings <= -50
	{
		"\1\s[10]\f[italic,true]You tell Otacon he's got nice shoes.\f[italic,default]\n\1\s[30]Um... Thank you?"
		"\1\s[13]\f[italic,true]You tell Otacon he's handsome. He looks visibly uncomfortable.\f[italic,false]\n... Okay. Thank you."
	}
	--
	"\e" //Closing up the dialogue.
}

//--- Joshing Otacon ------------------------------------------------------------
//Kind of random positive or negative boost. Some things go over Otacon's head.
Select.sayJokeKERO
{
	"\1\s[10]\f[italic,true]You tell Otacon a joke about " //Setting up the start of the joke.
	--
	if otaconFeelings > 40 //Adds these joke options to the pool if Otacon's feelings are over 40. Similar level system to compliment.
	{
		if RAND(100) > 29
		{
			"\1\s[10]using frying pans as drying pans.\f[italic,default]\n\n[half]\1\s[15]Hah! I still can't believe they put that in..."
			"\1\s[10]Snake hiding in a Cardboard Box Gundam.\f[italic,default]\0\s[8]\1\s[15]Hahaha! Oh my gosh, do you guys still talk about that in %(year)? I can't believe it."
			"\1\s[10]how they always shout 'It's a Gundam!'\1\s[16]\n\n[half]*Snrk*.\f[italic,default] a Gundam..!"
			--
			otaconFeelings += 3
			"\n\n[half]\1\s[10]That was a good one, %(username)."
		}
		else
		{
			"\1\s[10]the man who was beside himself when he was cloned.\f[italic,default]\0\s[4]\1\s[17]\n\n[half]That's rude! Snake is right here. And not how it works, anyway."
			--
			otaconFeelings += -3
			"\n\n[half]\f[italic,true]It seems Otacon didn't like that."
		}
		
	}
	if otaconFeelings > 20 //Ditto above, but 20 instead of 40.
	{
		if RAND(100) > 29
		{
			"\1\s[10]about how the news about the Origami museum is still unfolding.\1\s[16]\n\n[half]*Snrk*.\f[italic,default]"
			--
			otaconFeelings += 3
			"\n\n[half]\1\s[10]That one was pretty good."
		}
		else
		{
			"where Napoleon's armies go.\f[italic,default]\1\s[18]\n\n[half]Huh? Where's 'Sleevies'? Is it because he's short? Is everyone in Sleevies really short?"
			"why melons can't elope.\f[italic,default]\1\s[18]\n\n[half]What? Who would make a law against melon marriage? They can't even talk."
			--
			otaconFeelings += -3
			"\1\s[19]\w2.\w2\.\w2 I don't get it."
		}
	}
	if isNeko = 1 && RAND(100) > 89 //if Otacon is wearing the Neko accessory, and a random number from 0-99 is more than 89 (10% chance), then these jokes are added to the possible pool.
	{
		if RAND(100) > 30
		{
			"\1\s[10]how he must be purrfect.\f[italic,default]\1\s[30]\n\n[half]Aw, jeez, %(username). That's so cheesy I have to like it."
			"\1\s[10]the new Playstation for cats, the PSpspspsps.\n\n[half]\1\s[18]Otacon tilts his head and seems to focus entirely you for a moment at the sound.\f[italic,default]\n\n[half]\1\s[30]Oh! Uh, sorry. I, er... Hah."
			otaconFeelings += 3
		}
		else
		{
			"\1\s[10]how a cat took first place at a bird show.\f[italic,false]\n\n[half]\1\s[14]Poor little birdy! The cat's owner shouldn't have had him there at all...\1\s[33] That just made me sad."
			"\1\s[10]how the clownfish the cat ate tasted a little funny.\f[italic,default]\n\n[half]\1\s[14]Poor fishie! What an irresponsible fish owner."
			otaconFeelings += -3
		}
	}
	if otaconFeelings <= 19 //If Otacon's feelings are equal to or less than 19, these jokes will be added.
	{
		if RAND(100) > 49
		{
			"\1\s[10]how U R not \f[color,255,0,0]E\f[color,default].\f[italic,default]\n\n[half]Heh, I remember that ad campaign."
			--
			otaconFeelings += 3
			"\1\s[10] That was pretty alright."
		}
		else
		{
			"a fly with no wings.\f[italic,default]\1\s[17]\n\n[half]That's just sad. A fly who can only walk is going to starve to death..."
			"a fish with no Is.\f[italic,default]\1\s[17]\n\n[half]Now that's just a cruel joke! A fish with no eyes? Who would do such a thing? Poor little fishy."
			--
			otaconFeelings += -3
			"\1\s[10] I don't get why you'd say something like that."
		}
	}
	--
	"\e" //Closing up the dialogue.
}

//--Test Variables --------------------------------------------------------
//I briefly mentioned the test variable in bootend.dic and possibly in etc.dic as well, I'm not sure. What exactly is the test variable? Basically, it's a place you can plug in anything you want to test out. While you can use Script Input to test most of your ghost's dialogue, there are some lines it just can't do, and it can't help you test choices. But the test variable can!
//You can also use this to test functions as I mentioned above by adding \![raise,function] at the end of it, although you can do this with Script Input as well. Advanced Users, the test variable is also great for testing out any new stuff you may add to your ghost, like mode settings, item values, functions, raising or lowering numbers, what have you, particularly if you have a bunch you want to change all at once.

//It is ALWAYS A GOOD IDEA to test your dialogue! If you won't check it with the test variable, at least test it with Script Input! It'll save you so much trouble!
//OKUAJUB: the Test variable is currently commented out, but if you'd like to turn it back on, do whatever you'd like!

Select.testvariable
{
//Enter your dialogue to test here.
	"\1\s[10]Testing! Attention please!\n"
	--
	//"\1\s[10]Oh, you want me to bring out the MG MK. II? Sure!\p[3]\s[9910]"
}

//--Viewing Surfaces------------------------------------
Select.testKEROsurfaces //I know Surfacetest exists but simply cycling through the surfaces like this is helpful for me. It helps me notice things I'd overlook in Surfacetest.
{
	"\1\s[10]\w8\w8\w8\s[11]\w8\w8\w8\s[12]\w8\w8\w8\s[13]\w8\w8\w8\s[14]\w8\w8\w8\s[15]\w8\w8\w8\s[16]\w8\w8\w8\s[17]\w8\w8\w8\s[18]\w8\w8\w8\s[19]\w8\w8\w8\s[30]\w8\w8\w8\s[31]\w8\w8\w8\s[32]\w8\w8\w8\s[33]\e"
}
//--Viewing Variables-----------------------------------
Select.viewvariable
{
	"\1\s[18]codecTimer = %(codecTimer)\n/
	missionTimer = %(missionTimer)\n/
	inputTime = %(inputTime)\n/
	clickShadowMoses = %(clickShadowMoses)\n/
	clickShadowMosesIncident = %(clickShadowMosesIncident)\n/
	clickMeiLing = %(clickMeiLing) \n/
	clickRaiden = %(clickRaiden)\n/
	clickRex = %(clickRex)\n/
	snakeFeelings = %(snakeFeelings)\n/
	otaconFeelings = %(otaconFeelings)\n/
	snakeKetchups = %(snakeKetchups)\n/
	snakeApologies = %(snakeApologies)\n/
	\e"
}
//--Changing Variables after First Boot------------------
//OKUAJUB: This is a testing variable I've added so I can test variables without having to manually reset them using OnFirstBoot from bootend.dic - Accessed from Otacon's menu.
Select.changevariable
{

	codecTimer = ""
	missionTimer = ""
	clickShadowMoses = 0 //OKUAJUB: Click counting variable for Shadow Moses
	clickShadowMosesIncident = 0 //OKUAJUB: Click counting variable for Shadow Moses *incident*
	clickMeiLing = 0 //OKUAJUB: Click counting variable for Mei Ling
	clickRaiden = 0 //OKUAJUB: Click counting variable for Raiden
	clickJaeger = 0 //OKUAJUB: Click counting variable for Frank Jaeger
	clickBigBoss = 0 //OKUAJUBL Click counting variable for Big Boss
	clickRex = 0 //OKUAJUB: Click counting variable for Rex.
	snakeFeelings = -10 //OKUAJUB: Snake's feelings. its a bit harder to befriend Snake.
	otaconFeelings = 0 //OKUAJUB: Otacon's feelings. Neutral at first.
	snakeKetchups = 0 //OKUAJUB: Count of how many ketchup bottles you have given Snake.
	snakeApologies = 0 //OKUAJUB: How many times you have apologized to Snake.
	isNeko = 1
	
	"\1\s[10]Variables changed. Good luck testing, Okuajub! (Or are you %(username)..?)\n/
	codecTimer = %(codecTimer)\n/
	missionTimer = %(missionTimer)\n/
	clickShadowMoses = %(clickShadowMoses)\n/
	clickShadowMosesIncident = %(clickShadowMosesIncident)\n/
	clickMeiLing = %(clickMeiLing) \n/
	clickRaiden = %(clickRaiden)\n/
	clickJaeger = %(clickJaeger)\n/
	clickBigBoss = %(clickBigBoss)\n/
	clickRex = %(clickRex)\n/
	snakeFeelings = %(snakeFeelings)\n/
	otaconFeelings = %(otaconFeelings)\n/
	snakeKetchups = %(snakeKetchups)\n/
	snakeApologies = %(snakeApologies)\n/
	isNeko = %(isNeko)\n/
	\e"
}
